<!DOCTYPE html>
<html>
<head>
<meta charset="UTF-8">
<title>lang/minikanren/mk.org</title>
</head>
<body>
<pre style="white-space: pre-wrap;">#+PROPERTY: tangle mk.scm

* header

  #+begin_src scheme
  #!ikarus --r6rs-script
  #+end_src

* mk

  #+begin_src scheme
  (define-syntax lambdag@
    (syntax-rules ()
      ((_ (s) e) (lambda (s) e))))

  (define-syntax lambdaf@
    (syntax-rules ()
      ((_ () e) (lambda () e))))

  (define-syntax rhs
    (syntax-rules ()
      ((rhs p) (cdr p))))

  (define-syntax lhs
    (syntax-rules ()
      ((lhs p) (car p))))

  (define-syntax var
    (syntax-rules ()
      ((var w) (vector w))))

  (define-syntax var?
    (syntax-rules ()
      ((var? w) (vector? w))))

  (define-syntax size-s
    (syntax-rules ()
      ((size-s ls) (length ls))))

  (define empty-s &apos;())

  (define walk
    (lambda (v s)
      (cond
        ((var? v)
         (cond
           ((assq v s) =&gt;
            (lambda (a)
              (let ((v^ (rhs a)))
                (walk v^ s))))
           (else v)))
        (else v))))

  (define ext-s
    (lambda (x v s)
      (cons `(,x . ,v) s)))

  (define unify
    (lambda (v w s)
      (let ((v (walk v s))
            (w (walk w s)))
        (cond
          ((eq? v w) s)
          ((var? v) (ext-s v w s))
          ((var? w) (ext-s w v s))
          ((and (pair? v) (pair? w))
           (cond
             ((unify (car v) (car w) s) =&gt;
              (lambda (s)
                (unify (cdr v) (cdr w) s)))
             (else #f)))
          ((equal? v w) s)
          (else #f)))))

  (define ext-s-check
    (lambda (x v s)
      (cond
        ((occurs-check x v s) #f)
        (else (ext-s x v s)))))

  (define occurs-check
    (lambda (x v s)
      (let ((v (walk v s)))
        (cond
          ((var? v) (eq? v x))
          ((pair? v)
           (or
             (occurs-check x (car v) s)
             (occurs-check x (cdr v) s)))
          (else #f)))))

  (define unify-check
    (lambda (v w s)
      (let ((v (walk v s))
            (w (walk w s)))
        (cond
          ((eq? v w) s)
          ((var? v) (ext-s-check v w s))
          ((var? w) (ext-s-check w v s))
          ((and (pair? v) (pair? w))
           (cond
             ((unify-check (car v) (car w) s) =&gt;
              (lambda (s)
                (unify-check (cdr v) (cdr w) s)))
             (else #f)))
          ((equal? v w) s)
          (else #f)))))

  (define walk*
    (lambda (v s)
      (let ((v (walk v s)))
        (cond
          ((var? v) v)
          ((pair? v)
           (cons
             (walk* (car v) s)
             (walk* (cdr v) s)))
          (else v)))))

  (define reify-s
    (lambda (v s)
      (let ((v (walk v s)))
        (cond
          ((var? v) (ext-s v (reify-name (size-s s)) s))
          ((pair? v) (reify-s (cdr v) (reify-s (car v) s)))
          (else s)))))

  (define reify-name
    (lambda (n)
      (string-&gt;symbol
        (string-append &quot;_&quot; &quot;.&quot; (number-&gt;string n)))))

  (define reify
    (lambda (v)
      (walk* v (reify-s v empty-s))))

  (define-syntax run
    (syntax-rules ()
      ((_ n^ (x) g ...)
       (let ((n n^) (x (var &apos;x)))
         (if (or (not n) (&gt; n 0))
           (map-inf n
             (lambda (s) (reify (walk* x s)))
             ((all g ...) empty-s))
           &apos;())))))

  (define-syntax case-inf
    (syntax-rules ()
      ((_ e on-zero ((a^) on-one) ((a f) on-choice))
       (let ((a-inf e))
         (cond
           ((not a-inf) on-zero)
           ((not (and
                   (pair? a-inf)
                   (procedure? (cdr a-inf))))
            (let ((a^ a-inf))
              on-one))
           (else (let ((a (car a-inf))
                       (f (cdr a-inf)))
                   on-choice)))))))

  (define-syntax mzero
    (syntax-rules ()
      ((_) #f)))

  (define-syntax unit
    (syntax-rules ()
      ((_ a) a)))

  (define-syntax choice
    (syntax-rules ()
      ((_ a f) (cons a f))))

  (define map-inf
    (lambda (n p a-inf)
      (case-inf a-inf
        &apos;()
        ((a)
         (cons (p a) &apos;()))
        ((a f)
         (cons (p a)
           (cond
             ((not n) (map-inf n p (f)))
             ((&gt; n 1) (map-inf (- n 1) p (f)))
             (else &apos;())))))))

  (define ==
    (lambda (v w)
      (lambdag@ (s)
        (cond
          ((unify v w s) =&gt; succeed)
          (else (fail s))))))

  (define ==-check
    (lambda (v w)
      (lambdag@ (s)
        (cond
          ((unify-check v w s) =&gt; succeed)
          (else (fail s))))))

  (define-syntax fresh
    (syntax-rules ()
      ((_ (x ...) g ...)
       (lambdag@ (s)
         (let ((x (var &apos;x)) ...)
           ((all g ...) s))))))

  (define-syntax all
    (syntax-rules ()
      ((_) succeed)
      ((_ g) (lambdag@ (s) (g s)))
      ((_ g^ g ...) (lambdag@ (s) (bind (g^ s) (all g ...))))))

  (define-syntax conde
    (syntax-rules (else)
      ((_) fail)
      ((_ (else g0 g ...)) (all g0 g ...))
      ((_ (g0 g ...) c ...)
       (anye (all g0 g ...) (conde c ...)))))

  (define succeed (lambdag@ (s) (unit s)))

  (define fail (lambdag@ (s) (mzero)))

  (define bind
    (lambda (a-inf g)
      (case-inf a-inf
        (mzero)
        ((a) (g a))
        ((a f) (mplus (g a)
                 (lambdaf@ () (bind (f) g)))))))

  (define mplus
    (lambda (a-inf f)
      (case-inf a-inf
        (f)
        ((a) (choice a f))
        ((a f0) (choice a
                  (lambdaf@ () (mplus (f0) f)))))))

  (define-syntax anye
    (syntax-rules ()
      ((_ g1 g2)
       (lambdag@ (s)
         (mplus (g1 s)
           (lambdaf@ () (g2 s)))))))

  (define-syntax alli
    (syntax-rules ()
      ((_) succeed)
      ((_ g) (lambdag@ (s) (g s)))
      ((_ g^ g ...)
       (lambdag@ (s)
         (bindi (g^ s) (alli g ...))))))

  (define-syntax condi
    (syntax-rules (else)
      ((_) fail)
      ((_ (else g0 g ...)) (all g0 g ...))
      ((_ (g0 g ...) c ...)
       (anyi (all g0 g ...) (condi c ...)))))

  (define-syntax anyi
    (syntax-rules ()
      ((_ g1 g2)
       (lambdag@ (s)
         (mplusi (g1 s)
           (lambdaf@ () (g2 s)))))))

  (define bindi
    (lambda (a-inf g)
      (case-inf a-inf
        (mzero)
        ((a) (g a))
        ((a f) (mplusi (g a)
                 (lambdaf@ () (bindi (f) g)))))))

  (define mplusi
    (lambda (a-inf f)
      (case-inf a-inf
        (f)
        ((a) (choice a f))
        ((a f0) (choice a
                  (lambdaf@ () (mplusi (f) f0)))))))

  (define-syntax conda
    (syntax-rules (else)
      ((_) fail)
      ((_ (else g0 g ...)) (all g0 g ...))
      ((_ (g0 g ...) c ...)
       (ifa g0 (all g ...) (conda c ...)))))

  (define-syntax condu
    (syntax-rules (else)
      ((_) fail)
      ((_ (else g0 g ...)) (all g0 g ...))
      ((_ (g0 g ...) c ...)
       (ifu g0 (all g ...) (condu c ...)))))

  (define-syntax ifa
    (syntax-rules ()
      ((_ g0 g1 g2)
       (lambdag@ (s)
         (let ((s-inf (g0 s)) (g^ g1))
           (case-inf s-inf
             (g2 s)
             ((s) (g^ s))
             ((s f) (bind s-inf g^))))))))

  (define-syntax ifu
    (syntax-rules ()
      ((_ g0 g1 g2)
       (lambdag@ (s)
         (let ((s-inf (g0 s)) (g^ g1))
           (case-inf s-inf
             (g2 s)
             ((s) (g^ s))
             ((s f) (g^ s))))))))

  ;; ------

  (define-syntax run*
    (syntax-rules ()
      ((_ (x) g ...) (run #f (x) g ...))))

  (define-syntax lambda-limited
    (syntax-rules ()
      ((_ n formals g)
       (let ((x (var &apos;x)))
         (lambda formals
           (ll n x g))))))

  (define ll
    (lambda (n x g)
      (lambdag@ (s)
        (let ((v (walk x s)))
          (cond
            ((var? v) (g (ext-s x 1 s)))
            ((&lt; v n) (g (ext-s x (+ v 1) s)))
            (else (fail s)))))))

  (define-syntax project
    (syntax-rules ()
      ((_ (x ...) g ...)
       (lambdag@ (s)
         (let ((x (walk* x s)) ...)
           ((all g ...) s))))))
  #+end_src

* def

  #+begin_src scheme :tangle no
  (define caro
    (lambda (p a)
      (fresh (d)
        (== (cons a d) p))))

  (define cdro
    (lambda (p d)
      (fresh (a)
        (== (cons a d) p))))

  (define conso
    (lambda (a d p)
      (== (cons a d) p)))

  (define nullo
    (lambda (x)
      (== &apos;() x)))

  (define eqo
    (lambda (x y)
      (== x y)))

  (define eq-caro
    (lambda (l x)
      (caro l x)))

  (define pairo
    (lambda (p)
      (fresh (a d)
        (conso a d p))))

  (define listo
    (lambda (l)
      (conde
        ((nullo l) succeed)
        ((pairo l)
         (fresh (d)
           (cdro l d)
           (listo d)))
        (else fail))))

  (define membero
    (lambda (x l)
      (conde
        ((nullo l) fail)
        ((eq-caro l x) succeed)
        (else
         (fresh (d)
           (cdro l d)
           (membero x d))))))

  (define rembero
    (lambda (x l out)
      (conde
        ((nullo l) (== &apos;() out))
        ((eq-caro l x) (cdro l out))
        (else (fresh (a d res)
                (conso a d l)
                (rembero x d res)
                (conso a res out))))))

  (define appendo
    (lambda (l s out)
      (conde
        ((nullo l) (== s out))
        (else
         (fresh (a d res)
           (conso a d l)
           (conso a res out)
           (appendo d s res))))))

  (define anyo
    (lambda (g)
      (conde
        (g succeed)
        (else (anyo g)))))

  (define nevero (anyo fail))

  (define alwayso (anyo succeed))

  (define build-num
    (lambda (n)
      (cond
       ((zero? n) &apos;())
       ((and (not (zero? n)) (even? n))
        (cons 0
              (build-num (quotient n 2))))
       ((odd? n)
        (cons 1
              (build-num (quotient (- n 1) 2)))))))

  (define full-addero
    (lambda (b x y r c)
      (conde
        ((== 0 b) (== 0 x) (== 0 y) (== 0 r) (== 0 c))
        ((== 1 b) (== 0 x) (== 0 y) (== 1 r) (== 0 c))
        ((== 0 b) (== 1 x) (== 0 y) (== 1 r) (== 0 c))
        ((== 1 b) (== 1 x) (== 0 y) (== 0 r) (== 1 c))
        ((== 0 b) (== 0 x) (== 1 y) (== 1 r) (== 0 c))
        ((== 1 b) (== 0 x) (== 1 y) (== 0 r) (== 1 c))
        ((== 0 b) (== 1 x) (== 1 y) (== 0 r) (== 1 c))
        ((== 1 b) (== 1 x) (== 1 y) (== 1 r) (== 1 c))
        (else fail))))

  (define poso
    (lambda (n)
      (fresh (a d)
        (== `(,a . ,d) n))))

  (define &gt;1o
    (lambda (n)
      (fresh (a ad dd)
        (== `(,a ,ad . ,dd) n))))

  (define addero
    (lambda (d n m r)
      (condi
       ((== 0 d) (== &apos;() m) (== n r))
       ((== 0 d) (== &apos;() n) (== m r)
        (poso m))
       ((== 1 d) (== &apos;() m)
        (addero 0 n &apos;(1) r))
       ((== 1 d) (== &apos;() n) (poso m)
        (addero 0 &apos;(1) m r))
       ((== &apos;(1) n) (== &apos;(1) m)
        (fresh (a c)
          (== `(,a ,c) r)
          (full-addero d 1 1 a c)))
       ((== &apos;(1) n) (gen-addero d n m r))
       ((== &apos;(1) m) (&gt;1o n) (&gt;1o r)
        (addero d &apos;(1) n r))
       ((&gt;1o n) (gen-addero d n m r))
       (else fail))))

  (define gen-addero
    (lambda (d n m r)
      (fresh (a b c e x y z)
        (== `(,a . ,x) n)
        (== `(,b . ,y) m) (poso y)
        (== `(,c . ,z) r) (poso z)
        (alli
         (full-addero d a b c e)
         (addero e x y z)))))

  (define +o
    (lambda (n m k)
      (addero 0 n m k)))

  (define -o
    (lambda (n m k)
      (+o m k n)))

  (define *o
    (lambda (n m p)
      (condi
       ((== &apos;() n) (== &apos;() p))
       ((poso n) (== &apos;() m) (== &apos;() p))
       ((== &apos;(1) n) (poso m) (== m p))
       ((&gt;1o n) (== &apos;(1) m) (== n p))
       ((fresh (x z)
          (== `(0 . ,x) n) (poso x)
          (== `(0 . ,z) p) (poso z)
          (&gt;1o m)
          (*o x m z)))
       ((fresh (x y)
          (== `(1 . ,x) n) (poso x)
          (== `(0 . ,y) m) (poso y)
          (*o m n p)))
       ((fresh (x y)
          (== `(1 . ,x) n) (poso x)
          (== `(1 . ,y) m) (poso y)
          (odd-*o x n m p)))
       (else fail))))

  (define odd-*o
    (lambda (x n m p)
      (fresh (q)
        (bound-*o q p n m)
        (*o x m q)
        (+o `(0 . ,q) m p))))

  (define bound-*o
    (lambda (q p n m)
      (conde
        ((nullo q) (pairo p))
        (else
         (fresh (x y z)
           (cdro q x)
           (cdro p y)
           (condi
            ((nullo n)
             (cdro m z)
             (bound-*o x y z &apos;()))
            (else
             (cdro n z)
             (bound-*o x y z m))))))))

  (define =lo
    (lambda (n m)
      (conde
        ((== &apos;() n) (== &apos;() m))
        ((== &apos;(1) n) (== &apos;(1) m))
        (else
         (fresh (a x b y)
           (== `(,a . ,x) n) (poso x)
           (== `(,b . ,y) m) (poso y)
           (=lo x y))))))

  (define &lt;lo
    (lambda (n m)
      (conde
        ((== &apos;() n) (poso m))
        ((== &apos;(1) n) (&gt;1o m))
        (else
         (fresh (a x b y)
           (== `(,a . ,x) n) (poso x)
           (== `(,b . ,y) m) (poso y)
           (&lt;lo x y))))))

  (define &lt;=lo
    (lambda (n m)
      (condi
       ((=lo n m) succeed)
       ((&lt;lo n m) succeed)
       (else fail))))

  (define &lt;o
    (lambda (n m)
      (condi
       ((&lt;lo n m) succeed)
       ((=lo n m)
        (fresh (x)
          (poso x)
          (+o n x m)))
       (else fail))))

  (define &lt;=o
    (lambda (n m)
      (condi
       ((== n m) succeed)
       ((&lt;o n m) succeed)
       (else fail))))

  (define /o
    (lambda (n m q r)
      (condi
       ((== r n) (== &apos;() q) (&lt;o n m))
       ((== &apos;(1) q) (=lo n m) (+o r m n)
        (&lt;o r m))
       (else
        (alli
         (&lt;lo m n)
         (&lt;o r m)
         (poso q)
         (fresh (nh nl qh ql qlm qlmr rr rh)
           (alli
            (splito n r nl nh)
            (splito q r ql qh)
            (conde
              ((== &apos;() nh)
               (== &apos;() qh)
               (-o nl r qlm)
               (*o ql m qlm))
              (else
               (alli
                (poso nh)
                (*o ql m qlm)
                (+o qlm r qlmr)
                (-o qlmr nl rr)
                (splito rr r &apos;() rh)
                (/o nh m qh rh)))))))))))

  (define splito
    (lambda (n r l h)
      (condi
       ((== &apos;() n) (== &apos;() h) (== &apos;() l))
       ((fresh (b n^)
          (== `(0 ,b . ,n^) n)
          (== &apos;() r)
          (== `(,b . ,n^) h)
          (== &apos;() l)))
       ((fresh (n^)
          (==  `(1 . ,n^) n)
          (== &apos;() r)
          (== n^ h)
          (== &apos;(1) l)))
       ((fresh (b n^ a r^)
          (== `(0 ,b . ,n^) n)
          (== `(,a . ,r^) r)
          (== &apos;() l)
          (splito `(,b . ,n^) r^ &apos;() h)))
       ((fresh (n^ a r^)
          (== `(1 . ,n^) n)
          (== `(,a . ,r^) r)
          (== &apos;(1) l)
          (splito n^ r^ &apos;() h)))
       ((fresh (b n^ a r^ l^)
          (== `(,b . ,n^) n)
          (== `(,a . ,r^) r)
          (== `(,b . ,l^) l)
          (poso l^)
          (splito n^ r^ l^ h)))
       (else fail))))

  (define logo
    (lambda (n b q r)
      (condi
       ((== &apos;(1) n) (poso b) (== &apos;() q) (== &apos;() r))
       ((== &apos;() q) (&lt;o n b) (+o r &apos;(1) n))
       ((== &apos;(1) q) (&gt;1o b) (=lo n b) (+o r b n))
       ((== &apos;(1) b) (poso q) (+o r &apos;(1) n))
       ((== &apos;() b) (poso q) (== r n))
       ((== &apos;(0 1) b)
        (fresh (a ad dd)
          (poso dd)
          (== `(,a ,ad . ,dd) n)
          (exp2 n &apos;() q)
          (fresh (s)
            (splito n dd r s))))
       ((fresh (a ad add ddd)
          (conde
            ((== &apos;(1 1) b))
            (else (== `(,a ,ad ,add . ,ddd) b))))
        (&lt;lo b n)
        (fresh (bw1 bw nw nw1 ql1 ql s)
          (exp2 b &apos;() bw1)
          (+o bw1 &apos;(1) bw)
          (&lt;lo q n)
          (fresh (q1 bwq1)
            (+o q &apos;(1) q1)
            (*o bw q1 bwq1)
            (&lt;o nw1 bwq1))
          (exp2 n &apos;() nw1)
          (+o nw1 &apos;(1) nw)
          (/o nw bw ql1 s)
          (+o ql &apos;(1) ql1)
          (conde
            ((== q ql))
            (else (&lt;lo ql q)))
          (fresh (bql qh s qdh qd)
            (repeated-mul b ql bql)
            (/o nw bw1 qh s)
            (+o ql qdh qh)
            (+o ql qd q)
            (conde
              ((== qd qdh))
              (else (&lt;o qd qdh)))
            (fresh (bqd bq1 bq)
              (repeated-mul b qd bqd)
              (*o bql bqd bq)
              (*o b bq bq1)
              (+o bq r n)
              (&lt;o n bq1)))))
       (else fail))))

  (define exp2
    (lambda (n b q)
      (condi
       ((== &apos;(1) n) (== &apos;() q))
       ((&gt;1o n) (== &apos;(1) q)
        (fresh (s)
          (splito n b s &apos;(1))))
       ((fresh (q1 b2)
          (alli
           (== `(0 . ,q1) q)
           (poso q1)
           (&lt;lo b n)
           (appendo b `(1 . ,b) b2)
           (exp2 n b2 q1))))
       ((fresh (q1 nh b2 s)
          (alli
           (== `(1 . ,q1) q)
           (poso q1)
           (poso nh)
           (splito n b s nh)
           (appendo b `(1 . ,b) b2)
           (exp2 nh b2 q1))))
       (else fail))))

  (define repeated-mul
    (lambda (n q nq)
      (conde
        ((poso n) (== &apos;() q) (== &apos;(1) nq))
        ((== &apos;(1) q) (== n nq))
        ((&gt;1o q)
         (fresh (q1 nq1)
           (+o q1 &apos;(1) q)
           (repeated-mul n q1 nq1)
           (*o nq1 n nq)))
        (else fail))))

  (define expo
    (lambda (b q n)
      (logo n b q &apos;())))

  ;;;  &apos;trace-vars&apos; can be used to print the values of selected variables
  ;;;  in the substitution.
  (define-syntax trace-vars
    (syntax-rules ()
      ((_ title x ...)
       (lambdag@ (s)
                 (begin
                   (printf &quot;~a~n&quot; title)
                   (for-each (lambda (x_ t)
                               (printf &quot;~a = ~s~n&quot; x_ t))
                             `(x ...) (reify (walk* `(,x ...) s)))
                   (unit s))))))

  ;;; (run* (q)
  ;;;   (fresh (r)
  ;;;     (== 3 q)
  ;;;     (trace-vars &quot;What it is!&quot; q r)))
  ;;;
  ;;; What it is!
  ;;; q = 3
  ;;; r = _.0
  ;;; (3)
  #+end_src

* test

  #+begin_src scheme :tangle no
  (define-syntax test-check
    (syntax-rules ()
      ((_ title tested-expression expected-result)
       (begin
         (cout &quot;Testing &quot; title nl)
         (let* ((expected expected-result)
                (produced tested-expression))
           (or (equal? expected produced)
               (errorf &apos;test-check
                 &quot;Failed: ~a~%Expected: ~a~%Computed: ~a~%&quot;
                 &apos;tested-expression expected produced)))))))

  (define nl (string #\newline))

  (define (cout . args)
    (for-each (lambda (x)
                (if (procedure? x) (x) (display x)))
              args))

  (define errorf
    (lambda (tag . args)
      (display &quot;Failed: &quot;) (display tag) (newline)
      (for-each  display args)
      (error &apos;WiljaCodeTester &quot;That&apos;s all, folks!&quot;)))

  ;; (define-syntax test-divergence
  ;;   (syntax-rules ()
  ;;     ((_ title tested-expression)
  ;;      (let ((max-ticks 10000000))
  ;;        (cout &quot;Testing &quot; title &quot; (engine with &quot; max-ticks &quot; ticks fuel)&quot; nl)
  ;;        ((make-engine (lambda () tested-expression))
  ;;         max-ticks
  ;;         (lambda (t v)
  ;;           (errorf title
  ;;             &quot;infinite loop returned &quot; v &quot; after &quot; (- max-ticks t) &quot; ticks&quot;))
  ;;         (lambda (e^) (void)))))))

  ;;; Comment out this definition to test divergent code (Chez Scheme only)
  (define-syntax test-divergence
    (syntax-rules ()
      ((_ title tested-expression) (cout &quot;Ignoring divergent test &quot; title nl))))

  (test-check &quot;1.10&quot;
    (run* (q)
      fail)
    `())

  (test-check &quot;1.11&quot;
    (run* (q)
      (== #t q))
    `(#t))

  (test-check &quot;1.12&quot;
    (run* (q)
      fail
      (== #t q))
    `())

  (define g fail)

  (test-check &quot;1.13&quot;
    (run* (q)
      succeed
      (== #t q))
    (list #t))

  (test-check &quot;1.14&quot;
    (run* (q)
      succeed
      (== #t q))
    `(#t))

  (test-check &quot;1.15&quot;
    (run* (r)
      succeed
      (== &apos;corn r))
    (list &apos;corn))

  (test-check &quot;1.16&quot;
    (run* (r)
      succeed
      (== &apos;corn r))
    `(corn))

  (test-check &quot;1.17&quot;
    (run* (r)
      fail
      (== &apos;corn r))
    `())

  (test-check &quot;1.18&quot;
    (run* (q)
      succeed
      (== #f q))
    `(#f))

  (test-check &quot;1.22&quot;
    (run* (x)
      (let ((x #f))
        (== #t x)))
    &apos;())

  (test-check &quot;1.23&quot;
    (run* (q)
      (fresh (x)
        (== #t x)
        (== #t q)))
    (list #t))

  (test-check &quot;1.26&quot;
    (run* (q)
      (fresh (x)
        (== x #t)
        (== #t q)))
    (list #t))

  (test-check &quot;1.27&quot;
    (run* (q)
      (fresh (x)
        (== x #t)
        (== q #t)))
    (list #t))

  (test-check &quot;1.28&quot;
    (run* (x)
      succeed)
    (list `_.0))

  (test-check &quot;1.29&quot;
    (run* (x)
      (let ((x #f))
        (fresh (x)
          (== #t x))))
    `(_.0))

  (test-check &quot;1.30&quot;
    (run* (r)
      (fresh (x y)
        (== (cons x (cons y &apos;())) r)))
    (list `(_.0 _.1)))

  (test-check &quot;1.31&quot;
    (run* (s)
      (fresh (t u)
        (== (cons t (cons u &apos;())) s)))
    (list `(_.0 _.1)))

  (test-check &quot;1.32&quot;
    (run* (r)
      (fresh (x)
        (let ((y x))
          (fresh (x)
            (== (cons y (cons x (cons y &apos;()))) r)))))
    (list `(_.0 _.1 _.0)))

  (test-check &quot;1.33&quot;
    (run* (r)
      (fresh (x)
        (let ((y x))
          (fresh (x)
            (== (cons x (cons y (cons x &apos;()))) r)))))
    (list `(_.0 _.1 _.0)))

  (test-check &quot;1.34&quot;
    (run* (q)
      (== #f q)
      (== #t q))
    `())

  (test-check &quot;1.35&quot;
    (run* (q)
      (== #f q)
      (== #f q))
    &apos;(#f))

  (test-check &quot;1.36&quot;
    (run* (q)
      (let ((x q))
        (== #t x)))
    (list #t))

  (test-check &quot;1.37&quot;
    (run* (r)
      (fresh (x)
        (== x r)))
    (list `_.0))

  (test-check &quot;1.38&quot;
    (run* (q)
      (fresh (x)
        (== #t x)
        (== x q)))
    (list #t))

  (test-check &quot;1.39&quot;
    (run* (q)
      (fresh (x)
        (== x q)
        (== #t x)))
    (list #t))

  (test-check &quot;1.40.1&quot;
    (run* (q)
      (fresh (x)
        (== (eq? x q) q)))
    (list #f))

  (test-check &quot;1.40.2&quot;
    (run* (q)
      (let ((x q))
        (fresh (q)
          (== (eq? x q) x))))
    (list #f))

  (test-check &quot;1.41&quot;
    (cond
      (#f #t)
      (else #f))
    #f)

  (test-check &quot;1.43&quot;
    (cond
      (#f succeed)
      (else fail))
    fail)

  (test-check &quot;1.44&quot;
    (run* (q)
      (conde
        (fail succeed)
        (else fail)))
    &apos;())

  (test-check &quot;1.45&quot;
    (not (null? (run* (q)
                  (conde
                    (fail fail)
                    (else succeed)))))
    #t)

  (test-check &quot;1.46&quot;
    (not (null? (run* (q)
                  (conde
                    (succeed succeed)
                    (else fail)))))
    #t)


  (test-check &quot;1.47&quot;
    (run* (x)
      (conde
        ((== &apos;olive x) succeed)
        ((== &apos;oil x) succeed)
        (else fail)))
    `(olive oil))

  (test-check &quot;1.49&quot;
    (run 1 (x)
      (conde
        ((== &apos;olive x) succeed)
        ((== &apos;oil x) succeed)
        (else fail)))
    `(olive))

  (test-check &quot;1.50.1&quot;
    (run* (x)
      (conde
        ((== &apos;virgin x) fail)
        ((== &apos;olive x) succeed)
        (succeed succeed)
        ((== &apos;oil x) succeed)
        (else fail)))
    `(olive _.0 oil))

  (test-check &quot;1.50.2&quot;
    (run* (x)
      (conde
        ((== &apos;olive x) succeed)
        (succeed succeed)
        ((== &apos;oil x) succeed)
        (else fail)))
    `(olive _.0 oil))

  (test-check &quot;1.52&quot;
    (run 2 (x)
      (conde
        ((== &apos;extra x) succeed)
        ((== &apos;virgin x) fail)
        ((== &apos;olive x) succeed)
        ((== &apos;oil x) succeed)
        (else fail)))
    `(extra olive))

  (test-check &quot;1.53&quot;
    (run* (r)
      (fresh (x y)
        (== &apos;split x)
        (== &apos;pea y)
        (== (cons x (cons y &apos;())) r)))
    (list `(split pea)))

  (test-check &quot;1.54&quot;
    (run* (r)
      (fresh (x y)
        (conde
          ((== &apos;split x) (== &apos;pea y))
          ((== &apos;navy x) (== &apos;bean y))
          (else fail))
        (== (cons x (cons y &apos;())) r)))
    `((split pea) (navy bean)))

  (test-check &quot;1.55&quot;
    (run* (r)
      (fresh (x y)
        (conde
          ((== &apos;split x) (== &apos;pea y))
          ((== &apos;navy x) (== &apos;bean y))
          (else fail))
        (== (cons x (cons y (cons &apos;soup &apos;()))) r)))
    `((split pea soup) (navy bean soup)))

  ; 1.56
  (define teacupo
    (lambda (x)
      (conde
        ((== &apos;tea x) succeed)
        ((== &apos;cup x) succeed)
        (else fail))))

  (test-check &quot;1.56&quot;
    (run* (x)
      (teacupo x))
    `(tea cup))

  (test-check &quot;1.57&quot;
    (run* (r)
      (fresh (x y)
        (conde
          ((teacupo x) (== #t y) succeed)
          ((== #f x) (== #t y))
          (else fail))
        (== (cons x (cons y &apos;())) r)))
    `((tea #t) (cup #t) (#f #t)))

  (test-check &quot;1.58&quot;
    (run* (r)
      (fresh (x y z)
        (conde
          ((== y x) (fresh (x) (== z x)))
          ((fresh (x) (== y x)) (== z x))
          (else fail))
        (== (cons y (cons z &apos;())) r)))
    `((_.0 _.1) (_.0 _.1)))

  (test-check &quot;1.59&quot;
    (run* (r)
      (fresh (x y z)
        (conde
          ((== y x) (fresh (x) (== z x)))
          ((fresh (x) (== y x)) (== z x))
          (else fail))
        (== #f x)
        (== (cons y (cons z &apos;())) r)))
    `((#f _.0) (_.0 #f)))

  (test-check &quot;1.60&quot;
    (run* (q)
      (let ((a (== #t q))
            (b (== #f q)))
        b))
    &apos;(#f))

  (test-check &quot;1.61&quot;
    (run* (q)
      (let ((a (== #t q))
            (b (fresh (x)
                 (== x q)
                 (== #f x)))
            (c (conde
                 ((== #t q) succeed)
                 (else (== #f q)))))
        b))
    &apos;(#f))

  (test-check &quot;2.1&quot;
    (let ((x (lambda (a) a))
          (y &apos;c))
      (x y))
    &apos;c)

  (test-check &quot;2.2&quot;
    (run* (r)
      (fresh (y x)
        (== `(,x ,y) r)))
    (list `(_.0 _.1)))

  (test-check &quot;2.3&quot;
    (run* (r)
      (fresh (v w)
        (== (let ((x v) (y w)) `(,x ,y)) r)))
    `((_.0 _.1)))

  (test-check &quot;2.4&quot;
    (car `(grape raisin pear))
    `grape)

  (test-check &quot;2.5&quot;
    (car `(a c o r n))
    &apos;a)

  ; 2.9
  (define caro
    (lambda (p a)
      (fresh (d)
        (== (cons a d) p))))

  (test-check &quot;2.6&quot;
    (run* (r)
      (caro `(a c o r n) r))
    (list &apos;a))

  (test-check &quot;2.7&quot;
    (run* (q)
      (caro `(a c o r n) &apos;a)
      (== #t q))
    (list #t))

  (test-check &quot;2.8&quot;
    (run* (r)
      (fresh (x y)
        (caro `(,r ,y) x)
        (== &apos;pear x)))
    (list &apos;pear))

  (test-check &quot;2.10&quot;
    (cons
      (car `(grape raisin pear))
      (car `((a) (b) (c))))
    `(grape a))

  (test-check &quot;2.11&quot;
    (run* (r)
      (fresh (x y)
        (caro `(grape raisin pear) x)
        (caro `((a) (b) (c)) y)
        (== (cons x y) r)))
    (list `(grape a)))

  (test-check &quot;2.13&quot;
    (cdr `(grape raisin pear))
    `(raisin pear))

  (test-check &quot;2.14&quot;
    (car (cdr `(a c o r n)))
    &apos;c)

  ; 2.16
  (define cdro
    (lambda (p d)
      (fresh (a)
        (== (cons a d) p))))

  (test-check &quot;2.15&quot;
    (run* (r)
      (fresh (v)
        (cdro `(a c o r n) v)
        (caro v r)))
    (list &apos;c))

  (test-check &quot;2.17&quot;
    (cons
      (cdr `(grape raisin pear))
      (car `((a) (b) (c))))
    `((raisin pear) a))

  (test-check &quot;2.18&quot;
    (run* (r)
      (fresh (x y)
        (cdro `(grape raisin pear) x)
        (caro `((a) (b) (c)) y)
        (== (cons x y) r)))
    (list `((raisin pear) a)))

  (test-check &quot;2.19.1&quot;
    (run* (q)
      (cdro &apos;(a c o r n) &apos;(c o r n))
      (== #t q))
    (list #t))

  (test-check &quot;2.19.2&quot;
    `(c o r n)
    (cdr &apos;(a c o r n)))

  (test-check &quot;2.20.1&quot;
    (run* (x)
      (cdro &apos;(c o r n) `(,x r n)))
    (list &apos;o))

  (test-check &quot;2.20.2&quot;
    `(o r n)
    (cdr `(c o r n)))

  (test-check &quot;2.21&quot;
    (run* (l)
      (fresh (x)
        (cdro l &apos;(c o r n))
        (caro l x)
        (== &apos;a x)))
    (list `(a c o r n)))

  ; 2.28
  (define conso
    (lambda (a d p)
      (== (cons a d) p)))

  (test-check &quot;2.22&quot;
    (run* (l)
      (conso &apos;(a b c) &apos;(d e) l))
    (list `((a b c) d e)))

  (test-check &quot;2.23.1&quot;
    (run* (x)
      (conso x &apos;(a b c) &apos;(d a b c)))
    (list &apos;d))

  (test-check &quot;2.23.2&quot;
    (cons &apos;d &apos;(a b c))
    `(d a b c))

  (test-check &quot;2.24&quot;
    (run* (r)
      (fresh (x y z)
        (== `(e a d ,x) r)
        (conso y `(a ,z c) r)))
    (list `(e a d c)))

  (test-check &quot;2.25.1&quot;
    (run* (x)
      (conso x `(a ,x c) `(d a ,x c)))
    (list &apos;d))

  (define x &apos;d)

  (test-check &quot;2.25.2&quot;
    (cons x `(a ,x c))
    `(d a ,x c))

  (test-check &quot;2.26&quot;
    (run* (l)
      (fresh (x)
        (== `(d a ,x c) l)
        (conso x `(a ,x c) l)))
    (list `(d a d c)))

  (test-check &quot;2.27&quot;
    (run* (l)
      (fresh (x)
        (conso x `(a ,x c) l)
        (== `(d a ,x c) l)))
    (list `(d a d c)))

  (test-check &quot;2.29&quot;
    (run* (l)
      (fresh (d x y w s)
        (conso w &apos;(a n s) s)
        (cdro l s)
        (caro l x)
        (== &apos;b x)
        (cdro l d)
        (caro d y)
        (== &apos;e y)))
    (list `(b e a n s)))

  (test-check &quot;2.30&quot;
    (null? `(grape raisin pear))
    #f)

  (test-check &quot;2.31&quot;
    (null? &apos;())
    #t)

  ; 2.35
  (define nullo
    (lambda (x)
      (== &apos;() x)))

  (test-check &quot;2.32&quot;
    (run* (q)
      (nullo `(grape raisin pear))
      (== #t q))
    `())

  (test-check &quot;2.33&quot;
    (run* (q)
      (nullo &apos;())
      (== #t q))
    `(#t))

  (test-check &quot;2.34&quot;
    (run* (x)
      (nullo x))
    `(()))

  (test-check &quot;2.36&quot;
    (eq? &apos;pear &apos;plum)
    #f)

  (test-check &quot;2.37&quot;
    (eq? &apos;plum &apos;plum)
    #t)

  ; 2.40
  (define eqo
    (lambda (x y)
      (== x y)))

  (test-check &quot;2.38&quot;
    (run* (q)
      (eqo &apos;pear &apos;plum)
      (== #t q))
    `())

  (test-check &quot;2.39&quot;
    (run* (q)
      (eqo &apos;plum &apos;plum)
      (== #t q))
    `(#t))

  (test-check &quot;2.43&quot;
    (pair? `((split) . pea))
    #t)

  (test-check &quot;2.44&quot;
    (pair? &apos;())
    #f)

  (test-check &quot;2.48&quot;
    (car `(pear))
    `pear)

  (test-check &quot;2.49&quot;
    (cdr `(pear))
    `())

  (test-check &quot;2.51&quot;
    (cons `(split) &apos;pea)
    `((split) . pea))

  (test-check &quot;2.52&quot;
    (run* (r)
      (fresh (x y)
        (== (cons x (cons y &apos;salad)) r)))
    (list `(_.0 _.1 . salad)))

  ; 2.53
  (define pairo
    (lambda (p)
      (fresh (a d)
        (conso a d p))))

  (test-check &quot;2.54&quot;
    (run* (q)
      (pairo (cons q q))
      (== #t q))
    `(#t))

  (test-check &quot;2.55&quot;
    (run* (q)
      (pairo &apos;())
      (== #t q))
    `())

  (test-check &quot;2.56&quot;
    (run* (q)
      (pairo &apos;pair)
      (== #t q))
    `())

  (test-check &quot;2.57&quot;
    (run* (x)
      (pairo x))
    (list `(_.0 . _.1)))

  (test-check &quot;2.58&quot;
    (run* (r)
      (pairo (cons r &apos;pear)))
    (list `_.0))

  ; 3.1.1
  &apos;(define list?
    (lambda (l)
      (cond
        ((null? l) #t)
        ((pair? l) (list? (cdr l)))
        (else #f))))

  (test-check &quot;3.1.1&quot;
    (list? `((a) (a b) c))
    #t)

  (test-check &quot;3.2&quot;
    (list? `())
    #t)

  (test-check &quot;3.3&quot;
    (list? &apos;s)
    #f)

  (test-check &quot;3.4&quot;
    (list? `(d a t e . s))
    #f)

  ; 3.5
  (define listo
    (lambda (l)
      (conde
        ((nullo l) succeed)
        ((pairo l)
         (fresh (d)
           (cdro l d)
           (listo d)))
        (else fail))))

  (test-check &quot;3.7&quot;
    (run* (x)
      (listo `(a b ,x d)))
    (list `_.0))

  (test-check &quot;3.10&quot;
    (run 1 (x)
      (listo `(a b c . ,x)))
    (list `()))

  (test-divergence &quot;3.13&quot;
    (run* (x)
      (listo `(a b c . ,x))))

  (test-check &quot;3.14&quot;
    (run 5 (x)
      (listo `(a b c . ,x)))
    `(()
      (_.0)
      (_.0 _.1)
      (_.0 _.1 _.2)
      (_.0 _.1 _.2 _.3)))

  ; 3.16
  (define lol?
    (lambda (l)
      (cond
        ((null? l) #t)
        ((list? (car l)) (lol? (cdr l)))
        (else #f))))

  ; 3.17
  (define lolo
    (lambda (l)
      (conde
        ((nullo l) succeed)
        ((fresh (a)
           (caro l a)
           (listo a))
         (fresh (d)
           (cdro l d)
           (lolo d)))
        (else fail))))

  (test-check &quot;3.20&quot;
    (run 1 (l)
      (lolo l))
    `(()))

  (test-check &quot;3.21&quot;
    (run* (q)
      (fresh (x y)
        (lolo `((a b) (,x c) (d ,y)))
        (== #t q)))
    (list #t))

  (test-check &quot;3.22&quot;
    (run 1 (q)
      (fresh (x)
        (lolo `((a b) . ,x))
        (== #t q)))
    (list #t))

  (test-check &quot;3.23&quot;
    (run 1 (x)
      (lolo `((a b) (c d) . ,x)))
    `(()))

  (test-check &quot;3.24&quot;
    (run 5 (x)
      (lolo `((a b) (c d) . ,x)))
    `(()
      (())
      (() ())
      (() () ())
      (() () () ())))

  ; 3.31
  (define twinso
    (lambda (s)
      (fresh (x y)
        (conso x y s)
        (conso x &apos;() y))))

  (test-check &quot;3.32&quot;
    (run* (q)
      (twinso &apos;(tofu tofu))
      (== #t q))
    (list #t))

  (test-check &quot;3.33&quot;
    (run* (z)
      (twinso `(,z tofu)))
    (list `tofu))

  ; 3.36
  (define twinso
    (lambda (s)
      (fresh (x)
        (== `(,x ,x) s))))

  ; 3.37
  (define loto
    (lambda (l)
      (conde
        ((nullo l) succeed)
        ((fresh (a)
           (caro l a)
           (twinso a))
         (fresh (d)
           (cdro l d)
           (loto d)))
        (else fail))))

  (test-check &quot;3.38&quot;
    (run 1 (z)
      (loto `((g g) . ,z)))
    (list `()))

  (test-check &quot;3.42&quot;
    (run 5 (z)
      (loto `((g g) . ,z)))
    &apos;(()
      ((_.0 _.0))
      ((_.0 _.0) (_.1 _.1))
      ((_.0 _.0) (_.1 _.1) (_.2 _.2))
      ((_.0 _.0) (_.1 _.1) (_.2 _.2) (_.3 _.3))))

  (test-check &quot;3.45&quot;
    (run 5 (r)
      (fresh (w x y z)
        (loto `((g g) (e ,w) (,x ,y) . ,z))
        (== `(,w (,x ,y) ,z) r)))
    &apos;((e (_.0 _.0) ())
      (e (_.0 _.0) ((_.1 _.1)))
      (e (_.0 _.0) ((_.1 _.1) (_.2 _.2)))
      (e (_.0 _.0) ((_.1 _.1) (_.2 _.2) (_.3 _.3)))
      (e (_.0 _.0) ((_.1 _.1) (_.2 _.2) (_.3 _.3) (_.4 _.4)))))

  (test-check &quot;3.47&quot;
    (run 3 (out)
      (fresh (w x y z)
        (== `((g g) (e ,w) (,x ,y) . ,z) out)
        (loto out)))
    `(((g g) (e e) (_.0 _.0))
      ((g g) (e e) (_.0 _.0) (_.1 _.1))
      ((g g) (e e) (_.0 _.0) (_.1 _.1) (_.2 _.2))))

  ; 3.48
  (define listofo
    (lambda (predo l)
      (conde
        ((nullo l) succeed)
        ((fresh (a)
           (caro l a)
           (predo a))
         (fresh (d)
           (cdro l d)
           (listofo predo d)))
        (else fail))))

  (test-check &quot;3.49&quot;
    (run 3 (out)
      (fresh (w x y z)
        (== `((g g) (e ,w) (,x ,y) . ,z) out)
        (listofo twinso out)))
    `(((g g) (e e) (_.0 _.0))
      ((g g) (e e) (_.0 _.0) (_.1 _.1))
      ((g g) (e e) (_.0 _.0) (_.1 _.1) (_.2 _.2))))

  ; 3.50
  (define loto
    (lambda (l)
      (listofo twinso l)))

  ; 3.51.1
  (define member?
    (lambda (x l)
      (cond
        ((null? l) nil)
        ((eq-car? l x) #t)
        (else (member? x (cdr l))))))

  ; 3.51.2
  (define eq-car?
    (lambda (l x)
      (eq? (car l) x)))

  ; 3.53
  (test-check &quot;3-21&quot;
    (member? &apos;olive `(virgin olive oil))
    #t)

  ; 3.54.1
  (define eq-caro
    (lambda (l x)
      (caro l x)))

  ; 3.54.2
  (define membero
    (lambda (x l)
      (conde
        ((nullo l) fail)
        ((eq-caro l x) succeed)
        (else
          (fresh (d)
            (cdro l d)
            (membero x d))))))

  (test-check &quot;3.57&quot;
    (run* (q)
      (membero &apos;olive `(virgin olive oil))
      (== #t q))
    (list #t))

  (test-check &quot;3.58&quot;
    (run 1 (y)
      (membero y `(hummus with pita)))
    (list `hummus))

  (test-check &quot;3.59&quot;
    (run 1 (y)
      (membero y `(with pita)))
    (list `with))

  (test-check &quot;3.60&quot;
    (run 1 (y)
      (membero y `(pita)))
    (list `pita))

  (test-check &quot;3.61&quot;
    (run* (y)
      (membero y `()))
    `())

  (test-check &quot;3.62&quot;
    (run* (y)
      (membero y `(hummus with pita)))
    `(hummus with pita))

  ; 3.65
  (define identity
    (lambda (l)
      (run* (y)
        (membero y l))))

  (test-check &quot;3.66&quot;
    (run* (x)
      (membero &apos;e `(pasta ,x fagioli)))
    (list `e))

  (test-check &quot;3.69&quot;
    (run 1 (x)
      (membero &apos;e `(pasta e ,x fagioli)))
    (list `_.0))

  (test-check &quot;3.70&quot;
    (run 1 (x)
      (membero &apos;e `(pasta ,x e fagioli)))
    (list `e))

  (test-check &quot;3.71&quot;
    (run* (r)
      (fresh (x y)
        (membero &apos;e `(pasta ,x fagioli ,y))
        (== `(,x ,y) r)))
    `((e _.0) (_.0 e)))

  (test-check &quot;3.73&quot;
    (run 1 (l)
      (membero &apos;tofu l))
    `((tofu . _.0)))

  (test-divergence &quot;3.75&quot;
    (run* (l)
      (membero &apos;tofu l)))

  (test-check &quot;3.76&quot;
    (run 5 (l)
      (membero &apos;tofu l))
    `((tofu . _.0)
      (_.0 tofu . _.1)
      (_.0 _.1 tofu . _.2)
      (_.0 _.1 _.2 tofu . _.3)
      (_.0 _.1 _.2 _.3 tofu . _.4)))

  ; 3.80.1
  (define pmembero
    (lambda (x l)
      (conde
        ((nullo l) fail)
        ((eq-caro l x) (cdro l &apos;()))
        (else
          (fresh (d)
            (cdro l d)
            (pmembero x d))))))

  (test-check &quot;3.80.2&quot;
    (run 5 (l)
      (pmembero &apos;tofu l))
    `((tofu)
      (_.0 tofu)
      (_.0 _.1 tofu)
      (_.0 _.1 _.2 tofu)
      (_.0 _.1 _.2 _.3 tofu)))

  (test-check &quot;3.81&quot;
    (run* (q)
      (pmembero &apos;tofu `(a b tofu d tofu))
      (== #t q))
    `(#t))

  ; 3.83
  (define pmembero
    (lambda (x l)
      (conde
        ((nullo l) fail)
        ((eq-caro l x) (cdro l &apos;()))
        ((eq-caro l x) succeed)
        (else
          (fresh (d)
            (cdro l d)
            (pmembero x d))))))

  (test-check &quot;3.84&quot;
    (run* (q)
      (pmembero &apos;tofu `(a b tofu d tofu))
      (== #t q))
    `(#t #t #t))

  ; 3.86
  (define pmembero
    (lambda (x l)
      (conde
        ((nullo l) fail)
        ((eq-caro l x) (cdro l &apos;()))
        ((eq-caro l x)
         (fresh (a d)
           (cdro l `(,a . ,d))))
        (else
          (fresh (d)
            (cdro l d)
            (pmembero x d))))))

  (test-check &quot;3.88&quot;
    (run* (q)
      (pmembero &apos;tofu `(a b tofu d tofu))
      (== #t q))
    `(#t #t))

  (test-check &quot;3.89&quot;
    (run 12 (l)
      (pmembero &apos;tofu l))
    `((tofu)
      (tofu _.0 . _.1)
      (_.0 tofu)
      (_.0 tofu _.1 . _.2)
      (_.0 _.1 tofu)
      (_.0 _.1 tofu _.2 . _.3)
      (_.0 _.1 _.2 tofu)
      (_.0 _.1 _.2 tofu _.3 . _.4)
      (_.0 _.1 _.2 _.3 tofu)
      (_.0 _.1 _.2 _.3 tofu _.4 . _.5)
      (_.0 _.1 _.2 _.3 _.4 tofu)
      (_.0 _.1 _.2 _.3 _.4 tofu _.5 . _.6)))

  ; 3.93
  (define pmembero
    (lambda (x l)
      (conde
        ((eq-caro l x)
         (fresh (a d)
           (cdro l `(,a . ,d))))
        ((eq-caro l x) (cdro l &apos;()))
        (else
          (fresh (d)
            (cdro l d)
            (pmembero x d))))))

  (test-check &quot;3.94&quot;
    (run 12 (l)
      (pmembero &apos;tofu l))
    `((tofu _.0 . _.1)
      (tofu)
      (_.0 tofu _.1 . _.2)
      (_.0 tofu)
      (_.0 _.1 tofu _.2 . _.3)
      (_.0 _.1 tofu)
      (_.0 _.1 _.2 tofu _.3 . _.4)
      (_.0 _.1 _.2 tofu)
      (_.0 _.1 _.2 _.3 tofu _.4 . _.5)
      (_.0 _.1 _.2 _.3 tofu)
      (_.0 _.1 _.2 _.3 _.4 tofu _.5 . _.6)
      (_.0 _.1 _.2 _.3 _.4 tofu)))

  ; 3.95
  (define first-value
    (lambda (l)
      (run 1 (y)
        (membero y l))))

  (test-check &quot;3.96&quot;
    (first-value `(pasta e fagioli))
    `(pasta))

  (test-check &quot;3.97&quot;
    (first-value `(pasta e fagioli))
    (list `pasta))

  ; 3.98
  (define memberrevo
    (lambda (x l)
      (conde
        ((nullo l) fail)
        (succeed
          (fresh (d)
            (cdro l d)
            (memberrevo x d)))
        (else (eq-caro l x)))))

  (test-check &quot;3.100&quot;
    (run* (x)
      (memberrevo x `(pasta e fagioli)))
    `(fagioli e pasta))

  ; 3.101
  (define reverse-list
    (lambda (l)
      (run* (y)
        (memberrevo y l))))

  ; 4.1.1
  (define mem
    (lambda (x l)
      (cond
        ((null? l) #f)
        ((eq-car? l x) l)
        (else (mem x (cdr l))))))

  (test-check &quot;4.1.2&quot;
    (mem &apos;tofu `(a b tofu d peas e))
    `(tofu d peas e))

  (test-check &quot;4.2&quot;
    (mem &apos;tofu `(a b peas d peas e))
    #f)

  (test-check &quot;4.3&quot;
    (run* (out)
      (== (mem &apos;tofu `(a b tofu d peas e)) out))
    (list `(tofu d peas e)))

  (test-check &quot;4.4&quot;
    (mem &apos;peas (mem &apos;tofu `(a b tofu d peas e)))
    `(peas e))

  (test-check &quot;4.5&quot;
    (mem &apos;tofu (mem &apos;tofu `(a b tofu d tofu e)))
    `(tofu d tofu e))

  (test-check &quot;4.6&quot;
    (mem &apos;tofu (cdr (mem &apos;tofu `(a b tofu d tofu e))))
    `(tofu e))

  ; 4.7
  (define memo
    (lambda (x l out)
      (conde
        ((nullo l) fail)
        ((eq-caro l x) (== l out))
        (else
          (fresh (d)
            (cdro l d)
            (memo x d out))))))

  (test-check &quot;4.10&quot;
    (run 1 (out)
      (memo &apos;tofu `(a b tofu d tofu e) out))
    `((tofu d tofu e)))

  (test-check &quot;4.11&quot;
    (run 1 (out)
      (fresh (x)
        (memo &apos;tofu `(a b ,x d tofu e) out)))
    `((tofu d tofu e)))

  (test-check &quot;4.12&quot;
    (run* (r)
      (memo r `(a b tofu d tofu e) `(tofu d tofu e)))
    (list `tofu))

  (test-check &quot;4.13&quot;
    (run* (q)
      (memo &apos;tofu &apos;(tofu e) &apos;(tofu e))
      (== #t q))
    (list #t))

  (test-check &quot;4.14&quot;
    (run* (q)
      (memo &apos;tofu &apos;(tofu e) &apos;(tofu))
      (== #t q))
    `())

  (test-check &quot;4.15&quot;
    (run* (x)
      (memo &apos;tofu &apos;(tofu e) `(,x e)))
    (list `tofu))

  (test-check &quot;4.16&quot;
    (run* (x)
      (memo &apos;tofu &apos;(tofu e) `(peas ,x)))
    `())

  (test-check &quot;4.17&quot;
    (run* (out)
      (fresh (x)
        (memo &apos;tofu `(a b ,x d tofu e) out)))
    `((tofu d tofu e) (tofu e)))

  (test-check &quot;4.18&quot;
    (run 12 (z)
      (fresh (u)
        (memo &apos;tofu `(a b tofu d tofu e . ,z) u)))
    `(_.0
      _.0
      (tofu . _.0)
      (_.0 tofu . _.1)
      (_.0 _.1 tofu . _.2)
      (_.0 _.1 _.2 tofu . _.3)
      (_.0 _.1 _.2 _.3 tofu . _.4)
      (_.0 _.1 _.2 _.3 _.4 tofu . _.5)
      (_.0 _.1 _.2 _.3 _.4 _.5 tofu . _.6)
      (_.0 _.1 _.2 _.3 _.4 _.5 _.6 tofu . _.7)
      (_.0 _.1 _.2 _.3 _.4 _.5 _.6 _.7 tofu . _.8)
      (_.0 _.1 _.2 _.3 _.4 _.5 _.6 _.7 _.8 tofu . _.9)))

  ; 4.21
  (define memo
    (lambda (x l out)
      (conde
        ((eq-caro l x) (== l out))
        (else
          (fresh (d)
            (cdro l d)
            (memo x d out))))))

  ; 4.22
  (define rember
    (lambda (x l)
      (cond
        ((null? l) &apos;())
        ((eq-car? l x) (cdr l))
        (else
          (cons (car l)
            (rember x (cdr l)))))))

  (test-check &quot;4.23&quot;
    (rember &apos;peas &apos;(a b peas d peas e))
    `(a b d peas e))

  ; 4.24
  (define rembero
    (lambda (x l out)
      (conde
        ((nullo l) (== &apos;() out))
        ((eq-caro l x) (cdro l out))
        (else
          (fresh (res)
            (fresh (d)
              (cdro l d)
              (rembero x d res))
            (fresh (a)
              (caro l a)
              (conso a res out)))))))

  ; 4.27
  (define rembero
    (lambda (x l out)
      (conde
        ((nullo l) (== &apos;() out))
        ((eq-caro l x) (cdro l out))
        (else (fresh (a d res)
                (conso a d l)
                (rembero x d res)
                (conso a res out))))))

  (test-check &quot;4.30&quot;
    (run 1 (out)
      (fresh (y)
        (rembero &apos;peas `(a b ,y d peas e) out)))
    `((a b d peas e)))

  (test-check &quot;4.31&quot;
    (run* (out)
      (fresh (y z)
        (rembero y `(a b ,y d ,z e) out)))
    `((b a d _.0 e)
      (a b d _.0 e)
      (a b d _.0 e)
      (a b d _.0 e)
      (a b _.0 d e)
      (a b e d _.0)
      (a b _.0 d _.1 e)))

  (test-check &quot;4.49&quot;
    (run* (r)
      (fresh (y z)
        (rembero y `(,y d ,z e) `(,y d e))
        (== `(,y ,z) r)))
    `((d d)
      (d d)
      (_.0 _.0)
      (e e)))

  (test-check &quot;4.57&quot;
    (run 13 (w)
      (fresh (y z out)
        (rembero y `(a b ,y d ,z . ,w) out)))
    `(_.0
      _.0
      _.0
      _.0
      _.0
      ()
      (_.0 . _.1)
      (_.0)
      (_.0 _.1 . _.2)
      (_.0 _.1)
      (_.0 _.1 _.2 . _.3)
      (_.0 _.1 _.2)
      (_.0 _.1 _.2 _.3 . _.4)))

  ; 4.68
  (define surpriseo
    (lambda (s)
      (rembero s &apos;(a b c) &apos;(a b c))))

  (test-check &quot;4.69&quot;
    (run* (r)
      (== &apos;d r)
      (surpriseo r))
    (list &apos;d))

  (test-check &quot;4.70&quot;
    (run* (r)
      (surpriseo r))
    `(_.0))

  (test-check &quot;4.72&quot;
    (run* (r)
      (== &apos;b r)
      (surpriseo r))
    `(b))

  ; 5.2.1
  &apos;(define append
    (lambda (l s)
      (cond
        ((null? l) s)
        (else (cons (car l)
                (append (cdr l) s))))))

  (test-check &quot;5.2.2&quot;
    (append `(a b c) `(d e))
    `(a b c d e))

  (test-check &quot;5.3&quot;
    (append &apos;(a b c) &apos;())
    `(a b c))

  (test-check &quot;5.4&quot;
    (append &apos;() &apos;(d e))
    `(d e))

  (test-check &quot;5.6&quot;
    (append &apos;(d e) &apos;a)
    `(d e . a))

  ; 5.9
  (define appendo
    (lambda (l s out)
      (conde
        ((nullo l) (== s out))
        (else
          (fresh (a d res)
            (caro l a)
            (cdro l d)
            (appendo d s res)
            (conso a res out))))))

  (test-check &quot;5.10&quot;
    (run* (x)
      (appendo
       &apos;(cake)
       &apos;(tastes yummy)
       x))
    (list `(cake tastes yummy)))

  (test-check &quot;5.11&quot;
    (run* (x)
      (fresh (y)
        (appendo
         `(cake with ice ,y)
         &apos;(tastes yummy)
         x)))
    (list `(cake with ice _.0 tastes yummy)))

  (test-check &quot;5.12&quot;
    (run* (x)
      (fresh (y)
        (appendo
         &apos;(cake with ice cream)
         y
         x)))
    (list `(cake with ice cream . _.0)))

  (test-check &quot;5.13&quot;
    (run 1 (x)
      (fresh (y)
        (appendo `(cake with ice . ,y) &apos;(d t) x)))
    (list `(cake with ice d t)))

  (test-check &quot;5.14&quot;
    (run 1 (y)
      (fresh (x)
        (appendo `(cake with ice . ,y) &apos;(d t) x)))
    (list &apos;()))

  ; 5.15
  (define appendo
    (lambda (l s out)
      (conde
        ((nullo l) (== s out))
        (else
          (fresh (a d res)
            (conso a d l)
            (appendo d s res)
            (conso a res out))))))

  (test-check &quot;5.16&quot;
    (run 5 (x)
      (fresh (y)
        (appendo `(cake with ice . ,y) &apos;(d t) x)))
    `((cake with ice d t)
      (cake with ice _.0 d t)
      (cake with ice _.0 _.1 d t)
      (cake with ice _.0 _.1 _.2 d t)
      (cake with ice _.0 _.1 _.2 _.3 d t)))

  (test-check &quot;5.17&quot;
    (run 5 (y)
      (fresh (x)
        (appendo `(cake with ice . ,y) &apos;(d t) x)))
    `(()
      (_.0)
      (_.0 _.1)
      (_.0 _.1 _.2)
      (_.0 _.1 _.2 _.3)))

  (define y `(_.0 _.1 _.2))

  (test-check &quot;5.18&quot;
    `(cake with ice . ,y)
    `(cake with ice . (_.0 _.1 _.2)))

  (test-check &quot;5.20&quot;
    (run 5 (x)
      (fresh (y)
        (appendo
         `(cake with ice . ,y)
         `(d t . ,y)
         x)))
    `((cake with ice d t)
      (cake with ice _.0 d t _.0)
      (cake with ice _.0 _.1 d t _.0 _.1)
      (cake with ice _.0 _.1 _.2 d t _.0 _.1 _.2)
      (cake with ice _.0 _.1 _.2 _.3 d t _.0 _.1 _.2 _.3)))

  (test-check &quot;5.21&quot;
    (run* (x)
      (fresh (z)
        (appendo
         `(cake with ice cream)
         `(d t . ,z)
         x)))
    `((cake with ice cream d t . _.0)))

  (test-check &quot;5.23&quot;
    (run 6 (x)
      (fresh (y)
        (appendo x y `(cake with ice d t))))
    `(()
      (cake)
      (cake with)
      (cake with ice)
      (cake with ice d)
      (cake with ice d t)))

  (test-check &quot;5.25&quot;
    (run 6 (y)
      (fresh (x)
        (appendo x y `(cake with ice d t))))
    `((cake with ice d t)
      (with ice d t)
      (ice d t)
      (d t)
      (t)
      ()))

  ; 5.26.1
  (define appendxyquestion
    (lambda ()
      (run 6 (r)
        (fresh (x y)
          (appendo x y `(cake with ice d t))
          (== `(,x ,y) r)))))

  ; 5.26.2
  (define appendxyanswer
    `((() (cake with ice d t))
      ((cake) (with ice d t))
      ((cake with) (ice d t))
      ((cake with ice) (d t))
      ((cake with ice d) (t))
      ((cake with ice d t) ())))

  (test-check &quot;appendxy&quot;
    (appendxyquestion)
    appendxyanswer)

  (test-divergence &quot;5.29&quot;
    (run 7 (r)
      (fresh (x y)
        (appendo x y `(cake with ice d t))
        (== `(,x ,y) r))))

  ; 5.31
  (define appendo
    (lambda (l s out)
      (conde
        ((nullo l) (== s out))
        (else
          (fresh (a d res)
            (conso a d l)
            (conso a res out)
            (appendo d s res))))))

  (test-check &quot;5.32&quot;
    (run 7 (r)
      (fresh (x y)
        (appendo x y `(cake with ice d t))
        (== `(,x ,y) r)))
    appendxyanswer)

  (test-check &quot;5.33&quot;
    (run 7 (x)
      (fresh (y z)
        (appendo x y z)))
    `(()
      (_.0)
      (_.0 _.1)
      (_.0 _.1 _.2)
      (_.0 _.1 _.2 _.3)
      (_.0 _.1 _.2 _.3 _.4)
      (_.0 _.1 _.2 _.3 _.4 _.5)))

  (test-check &quot;5.34&quot;
    (run 7 (y)
      (fresh (x z)
        (appendo x y z)))
    `(_.0
      _.0
      _.0
      _.0
      _.0
      _.0
      _.0))

  (test-check &quot;5.36&quot;
    (run 7 (z)
      (fresh (x y)
        (appendo x y z)))
    `(_.0
      (_.0 . _.1)
      (_.0 _.1 . _.2)
      (_.0 _.1 _.2 . _.3)
      (_.0 _.1 _.2 _.3 . _.4)
      (_.0 _.1 _.2 _.3 _.4 . _.5)
      (_.0 _.1 _.2 _.3 _.4 _.5 . _.6)))

  (test-check &quot;5.37&quot;
    (run 7 (r)
      (fresh (x y z)
        (appendo x y z)
        (== `(,x ,y ,z) r)))
    `((() _.0 _.0)
      ((_.0) _.1 (_.0 . _.1))
      ((_.0 _.1) _.2 (_.0 _.1 . _.2))
      ((_.0 _.1 _.2) _.3 (_.0 _.1 _.2 . _.3))
      ((_.0 _.1 _.2 _.3) _.4 (_.0 _.1 _.2 _.3 . _.4))
      ((_.0 _.1 _.2 _.3 _.4) _.5 (_.0 _.1 _.2 _.3 _.4 . _.5))
      ((_.0 _.1 _.2 _.3 _.4 _.5) _.6 (_.0 _.1 _.2 _.3 _.4 _.5 . _.6))))

  ; 5.38
  (define swappendo
    (lambda (l s out)
      (conde
        (succeed
          (fresh (a d res)
            (conso a d l)
            (conso a res out)
            (swappendo d s res)))
        (else (nullo l) (== s out)))))

  (test-divergence &quot;5.39&quot;
    (run 1 (z)
      (fresh (x y)
        (swappendo x y z))))

  ; 5.41.1
  (define unwrap
    (lambda (x)
      (cond
        ((pair? x) (unwrap (car x)))
        (else x))))

  (test-check &quot;5.41.2&quot;
    (unwrap &apos;((((pizza)))))
    `pizza)

  (test-check &quot;5.42&quot;
    (unwrap &apos;((((pizza pie) with)) extra cheese))
    `pizza)

  ; 5.45
  (define unwrapo
    (lambda (x out)
      (conde
        ((pairo x)
         (fresh (a)
           (caro x a)
           (unwrapo a out)))
        (else (== x out)))))

  (test-check &quot;5.46&quot;
    (run* (x)
      (unwrapo &apos;(((pizza))) x))
    `(pizza
      (pizza)
      ((pizza))
      (((pizza)))))

  (test-divergence &quot;5.48&quot;
    (run 1 (x)
      (unwrapo x &apos;pizza)))

  (test-divergence &quot;5.49&quot;
    (run 1 (x)
      (unwrapo `((,x)) &apos;pizza)))

  ; 5.52
  (define unwrapo
    (lambda (x out)
      (conde
        (succeed (== x out))
        (else
          (fresh (a)
            (caro x a)
            (unwrapo a out))))))

  (test-check &quot;5.53&quot;
    (run 5 (x)
      (unwrapo x &apos;pizza))
    `(pizza
      (pizza . _.0)
      ((pizza . _.0) . _.1)
      (((pizza . _.0) . _.1) . _.2)
      ((((pizza . _.0) . _.1) . _.2) . _.3)))

  (test-check &quot;5.54&quot;
    (run 5 (x)
      (unwrapo x &apos;((pizza))))
    `(((pizza))
      (((pizza)) . _.0)
      ((((pizza)) . _.0) . _.1)
      (((((pizza)) . _.0) . _.1) . _.2)
      ((((((pizza)) . _.0) . _.1) . _.2) . _.3)))

  (test-check &quot;5.55&quot;
    (run 5 (x)
      (unwrapo `((,x)) &apos;pizza))
    `(pizza
      (pizza . _.0)
      ((pizza . _.0) . _.1)
      (((pizza . _.0) . _.1) . _.2)
      ((((pizza . _.0) . _.1) . _.2) . _.3)))

  ; 5.58.1
  (define flatten
    (lambda (s)
      (cond
        ((null? s) &apos;())
        ((pair? s)
         (append
           (flatten (car s))
           (flatten (cdr s))))
        (else (cons s &apos;())))))

  (test-check &quot;5.58.1&quot;
    (flatten &apos;((a b) c))
    `(a b c))

  ; 5.59
  (define flatteno
    (lambda (s out)
      (conde
        ((nullo s) (== &apos;() out))
        ((pairo s)
         (fresh (a d res-a res-d)
           (conso a d s)
           (flatteno a res-a)
           (flatteno d res-d)
           (appendo res-a res-d out)))
        (else (conso s &apos;() out)))))

  (test-check &quot;5.60&quot;
    (run 1 (x)
      (flatteno &apos;((a b) c) x))
    (list `(a b c)))

  (test-check &quot;5.61&quot;
    (run 1 (x)
      (flatteno &apos;(a (b c)) x))
    (list `(a b c)))

  (test-check &quot;5.62&quot;
    (run* (x)
      (flatteno &apos;(a) x))
    `((a)
      (a ())
      ((a))))

  (test-check &quot;5.64&quot;
    (run* (x)
      (flatteno &apos;((a)) x))
    `((a)
      (a ())
      (a ())
      (a () ())
      ((a))
      ((a) ())
      (((a)))))

  (test-check &quot;5.66&quot;
    (run* (x)
      (flatteno &apos;(((a))) x))
    `((a)
      (a ())
      (a ())
      (a () ())
      (a ())
      (a () ())
      (a () ())
      (a () () ())
      ((a))
      ((a) ())
      ((a) ())
      ((a) () ())
      (((a)))
      (((a)) ())
      ((((a))))))

  ; 5.68.1
  (define flattenogrumblequestion
    (lambda ()
      (run* (x)
        (flatteno &apos;((a b) c) x)) ))

  ; 5.68.2
  (define flattenogrumbleanswer
    `((a b c)
      (a b c ())
      (a b (c))
      (a b () c)
      (a b () c ())
      (a b () (c))
      (a (b) c)
      (a (b) c ())
      (a (b) (c))
      ((a b) c)
      ((a b) c ())
      ((a b) (c))
      (((a b) c))))

  (test-check &quot;flattenogrumble&quot;
    (flattenogrumblequestion)
    flattenogrumbleanswer)

  (test-divergence &quot;5.71&quot;
    (run* (x)
      (flatteno x &apos;(a b c))))

  ; 5.73
  (define flattenrevo
    (lambda (s out)
      (conde
        (succeed (conso s &apos;() out))
        ((nullo s) (== &apos;() out))
        (else
          (fresh (a d res-a res-d)
            (conso a d s)
            (flattenrevo a res-a)
            (flattenrevo d res-d)
            (appendo res-a res-d out))))))

  (test-check &quot;5.75&quot;
    (run* (x)
      (flattenrevo &apos;((a b) c) x))
    `((((a b) c))
      ((a b) (c))
      ((a b) c ())
      ((a b) c)
      (a (b) (c))
      (a (b) c ())
      (a (b) c)
      (a b () (c))
      (a b () c ())
      (a b () c)
      (a b (c))
      (a b c ())
      (a b c)))

  (test-check &quot;5.76&quot;
    (reverse
      (run* (x)
        (flattenrevo &apos;((a b) c) x)))
    flattenogrumbleanswer)

  (test-check &quot;5.77&quot;
    (run 2 (x)
      (flattenrevo x &apos;(a b c)))
    `((a b . c)
      (a b c)))

  (test-divergence &quot;5.79&quot;
    (run 3 (x)
      (flattenrevo x &apos;(a b c))))

  (test-check &quot;5.80&quot;
    (length
      (run* (x)
        (flattenrevo &apos;((((a (((b))) c))) d) x)))
    574)

  ; 6.1
  (define anyo
    (lambda (g)
      (conde
        (g succeed)
        (else (anyo g)))))

  ; 6.4
  (define nevero (anyo fail))

  (test-divergence &quot;6.5&quot;
    (run 1 (q)
      nevero
      (== #t q)))

  ; 6.7
  (define alwayso (anyo succeed))

  (test-check &quot;6.7&quot;
    (run 1 (q)
      alwayso
      (== #t q))
    (list #t))

  (test-divergence &quot;6.9&quot;
    (run* (q)
      alwayso
      (== #t q)))

  (test-check &quot;6.10&quot;
    (run 5 (q)
      alwayso
      (== #t q))
    `(#t #t #t #t #t))

  (test-check &quot;6.11&quot;
    (run 5 (q)
      (== #t q)
      alwayso)
    `(#t #t #t #t #t))

  ; 6.12
  (define salo
    (lambda (g)
      (conde
        (succeed succeed)
        (else g))))

  (test-check &quot;6.13&quot;
    (run 1 (q)
      (salo alwayso)
      (== #t q))
    `(#t))

  (test-check &quot;6.14&quot;
    (run 1 (q)
      (salo nevero)
      (== #t q))
    `(#t))

  (test-divergence &quot;6.15&quot;
    (run* (q)
      (salo nevero)
      (== #t q)))

  (test-divergence &quot;6.16&quot;
    (run 1 (q)
      (salo nevero)
      fail
      (== #t q)))

  (test-divergence &quot;6.17&quot;
    (run 1 (q)
      alwayso
      fail
      (== #t q)))

  (test-divergence &quot;6.18&quot;
    (run 1 (q)
      (conde
        ((== #f q) alwayso)
        (else (anyo (== #t q))))
      (== #t q)))

  (test-check &quot;6.19&quot;
    (run 1 (q)
      (condi
        ((== #f q) alwayso)
        (else (== #t q)))
      (== #t q))
    `(#t))

  (test-divergence &quot;6.20&quot;
    (run 2 (q)
      (condi
        ((== #f q) alwayso)
        (else (== #t q)))
      (== #t q)))

  (test-check &quot;6.21&quot;
    (run 5 (q)
      (condi
        ((== #f q) alwayso)
        (else (anyo (== #t q))))
      (== #t q))
    `(#t #t #t #t #t))

  (test-check &quot;6.24&quot;
    (run 5 (r)
      (condi
        ((teacupo r) succeed)
        ((== #f r) succeed)
        (else fail)))
    `(tea #f cup))

  (test-check &quot;6.25&quot;
    (run 5 (q)
      (condi
        ((== #f q) alwayso)
        ((== #t q) alwayso)
        (else fail))
      (== #t q))
    `(#t #t #t #t #t))

  (test-divergence &quot;6.27&quot;
    (run 5 (q)
      (conde
        ((== #f q) alwayso)
        ((== #t q) alwayso)
        (else fail))
      (== #t q)))

  (test-check &quot;6.28&quot;
    (run 5 (q)
      (conde
        (alwayso succeed)
        (else nevero))
      (== #t q))
    `(#t #t #t #t #t))

  (test-divergence &quot;6.30&quot;
    (run 5 (q)
      (condi
        (alwayso succeed)
        (else nevero))
      (== #t q)))

  (test-divergence &quot;6.31&quot;
    (run 1 (q)
      (all
        (conde
          ((== #f q) succeed)
          (else (== #t q)))
        alwayso)
      (== #t q)))

  (test-check &quot;6.32&quot;
    (run 1 (q)
      (alli
        (conde
          ((== #f q) succeed)
          (else (== #t q)))
        alwayso)
      (== #t q))
    `(#t))

  (test-check &quot;6.33&quot;
    (run 5 (q)
      (alli
        (conde
          ((== #f q) succeed)
          (else (== #t q)))
        alwayso)
      (== #t q))
    `(#t #t #t #t #t))

  (test-check &quot;6.34&quot;
    (run 5 (q)
      (alli
        (conde
          ((== #t q) succeed)
          (else (== #f q)))
        alwayso)
      (== #t q))
    `(#t #t #t #t #t))

  (test-check &quot;6.36&quot;
    (run 5 (q)
      (all
        (conde
          (succeed succeed)
          (else nevero))
        alwayso)
      (== #t q))
    `(#t #t #t #t #t))

  (test-divergence &quot;6.38&quot;
    (run 5 (q)
      (alli
        (conde
          (succeed succeed)
          (else nevero))
        alwayso)
      (== #t q)))

  ; 7.5
  (define bit-xoro
    (lambda (x y r)
      (conde
        ((== 0 x) (== 0 y) (== 0 r))
        ((== 1 x) (== 0 y) (== 1 r))
        ((== 0 x) (== 1 y) (== 1 r))
        ((== 1 x) (== 1 y) (== 0 r))
        (else fail))))

  (test-check &quot;7.6&quot;
    (run* (s)
      (fresh (x y)
        (bit-xoro x y 0)
        (== `(,x ,y) s)))
    `((0 0)
      (1 1)))

  (test-check &quot;7.8&quot;
    (run* (s)
      (fresh (x y)
        (bit-xoro x y 1)
        (== `(,x ,y) s)))
    `((1 0)
      (0 1)))

  (test-check &quot;7.9&quot;
    (run* (s)
      (fresh (x y r)
        (bit-xoro x y r)
        (== `(,x ,y ,r) s)))
    `((0 0 0)
      (1 0 1)
      (0 1 1)
      (1 1 0)))

  ; 7.10
  (define bit-ando
    (lambda (x y r)
      (conde
        ((== 0 x) (== 0 y) (== 0 r))
        ((== 1 x) (== 0 y) (== 0 r))
        ((== 0 x) (== 1 y) (== 0 r))
        ((== 1 x) (== 1 y) (== 1 r))
        (else fail))))

  (test-check &quot;7.11&quot;
    (run* (s)
      (fresh (x y)
        (bit-ando x y 1)
        (== `(,x ,y) s)))
    `((1 1)))

  ; 7.12.1
  (define half-addero
    (lambda (x y r c)
      (all
        (bit-xoro x y r)
        (bit-ando x y c))))

  (test-check &quot;7.12.2&quot;
    (run* (r)
      (half-addero 1 1 r 1))
    (list 0))

  (test-check &quot;7.13&quot;
    (run* (s)
      (fresh (x y r c)
        (half-addero x y r c)
        (== `(,x ,y ,r ,c) s)))
    `((0 0 0 0)
      (1 0 1 0)
      (0 1 1 0)
      (1 1 0 1)))

  ; 7.15.1
  (define full-addero
    (lambda (b x y r c)
      (fresh (w xy wz)
        (half-addero x y w xy)
        (half-addero w b r wz)
        (bit-xoro xy wz c))))

  (test-check &quot;7.15.2&quot;
    (run* (s)
      (fresh (r c)
        (full-addero 0 1 1 r c)
        (== `(,r ,c) s)))
    (list `(0 1)))

  ; 7.15.3
  (define full-addero
    (lambda (b x y r c)
      (conde
        ((== 0 b) (== 0 x) (== 0 y) (== 0 r) (== 0 c))
        ((== 1 b) (== 0 x) (== 0 y) (== 1 r) (== 0 c))
        ((== 0 b) (== 1 x) (== 0 y) (== 1 r) (== 0 c))
        ((== 1 b) (== 1 x) (== 0 y) (== 0 r) (== 1 c))
        ((== 0 b) (== 0 x) (== 1 y) (== 1 r) (== 0 c))
        ((== 1 b) (== 0 x) (== 1 y) (== 0 r) (== 1 c))
        ((== 0 b) (== 1 x) (== 1 y) (== 0 r) (== 1 c))
        ((== 1 b) (== 1 x) (== 1 y) (== 1 r) (== 1 c))
        (else fail))))

  (test-check &quot;7.16&quot;
    (run* (s)
      (fresh (r c)
        (full-addero 1 1 1 r c)
        (== `(,r ,c) s)))
    (list `(1 1)))

  (test-check &quot;7.17&quot;
    (run* (s)
      (fresh (b x y r c)
        (full-addero b x y r c)
        (== `(,b ,x ,y ,r ,c) s)))
    `((0 0 0 0 0)
      (1 0 0 1 0)
      (0 1 0 1 0)
      (1 1 0 0 1)
      (0 0 1 1 0)
      (1 0 1 0 1)
      (0 1 1 0 1)
      (1 1 1 1 1)))

  ; 7.43
  (define build-num
    (lambda (n)
      (cond
        ((zero? n) &apos;())
        ((and (not (zero? n)) (even? n))
         (cons 0
           (build-num (quotient n 2))))
        ((odd? n)
         (cons 1
           (build-num (quotient (- n 1) 2)))))))

  (test-check &quot;7.25&quot;
    `(1 0 1)
    (build-num 5))

  (test-check &quot;7.26&quot;
    `(1 1 1)
    (build-num 7))

  (test-check &quot;7.27&quot;
    (build-num 9)
    `(1 0 0 1))

  (test-check &quot;7.28&quot;
    (build-num 6)
    `(0 1 1))

  (test-check &quot;7.31&quot;
    (build-num 19)
    `(1 1 0 0 1))

  (test-check &quot;7.32&quot;
    (build-num 17290)
    `(0 1 0 1 0 0 0 1 1 1 0 0 0 0 1))

  (test-check &quot;7.40&quot;
    (build-num 0)
    `())

  (test-check &quot;7.41&quot;
    (build-num 36)
    `(0 0 1 0 0 1))

  (test-check &quot;7.42&quot;
    (build-num 19)
    `(1 1 0 0 1))

  ; 7.44
  (define build-num
    (lambda (n)
      (cond
        ((odd? n)
         (cons 1
           (build-num (quotient (- n 1) 2))))
        ((and (not (zero? n)) (even? n))
         (cons 0
           (build-num (quotient n 2))))
        ((zero? n) &apos;()))))

  ; 7.80.1
  (define poso
    (lambda (n)
      (fresh (a d)
        (== `(,a . ,d) n))))

  (test-check &quot;7.80.2&quot;
    (run* (q)
      (poso &apos;(0 1 1))
      (== #t q))
    (list #t))

  (test-check &quot;7.81&quot;
    (run* (q)
      (poso &apos;(1))
      (== #t q))
    (list #t))

  (test-check &quot;7.82&quot;
    (run* (q)
      (poso &apos;())
      (== #t q))
    `())

  (test-check &quot;7.83&quot;
    (run* (r)
      (poso r))
    (list `(_.0 . _.1)))

  ; 7.86.1
  (define &gt;1o
    (lambda (n)
      (fresh (a ad dd)
        (== `(,a ,ad . ,dd) n))))

  (test-check &quot;7.86.2&quot;
    (run* (q)
      (&gt;1o &apos;(0 1 1))
      (== #t q))
    (list #t))

  (test-check &quot;7.87&quot;
    (run* (q)
      (&gt;1o &apos;(0 1))
      (== #t q))
    `(#t))

  (test-check &quot;7.88&quot;
    (run* (q)
      (&gt;1o &apos;(1))
      (== #t q))
    `())

  (test-check &quot;7.89&quot;
    (run* (q)
      (&gt;1o &apos;())
      (== #t q))
    `())

  (test-check &quot;7.90&quot;
    (run* (r)
      (&gt;1o r))
    (list `(_.0 _.1 . _.2)))

  ; 7.118.1
  (define addero
    (lambda (d n m r)
      (condi
        ((== 0 d) (== &apos;() m) (== n r))
        ((== 0 d) (== &apos;() n) (== m r)
         (poso m))
        ((== 1 d) (== &apos;() m)
         (addero 0 n &apos;(1) r))
        ((== 1 d) (== &apos;() n) (poso m)
         (addero 0 &apos;(1) m r))
        ((== &apos;(1) n) (== &apos;(1) m)
         (fresh (a c)
           (== `(,a ,c) r)
           (full-addero d 1 1 a c)))
        ((== &apos;(1) n) (gen-addero d n m r))
        ((== &apos;(1) m) (&gt;1o n) (&gt;1o r)
         (addero d &apos;(1) n r))
        ((&gt;1o n) (gen-addero d n m r))
        (else fail))))

  ; 7.118.2
  (define gen-addero
    (lambda (d n m r)
      (fresh (a b c e x y z)
        (== `(,a . ,x) n)
        (== `(,b . ,y) m) (poso y)
        (== `(,c . ,z) r) (poso z)
        (alli
          (full-addero d a b c e)
          (addero e x y z)))))

  (test-check &quot;7.97&quot;
    (run 3 (s)
      (fresh (x y r)
        (addero 0 x y r)
        (== `(,x ,y ,r) s)))
    `((_.0 () _.0)
      (() (_.0 . _.1) (_.0 . _.1))
      ((1) (1) (0 1))))

  (test-check &quot;7.101&quot;
    (run 22 (s)
      (fresh (x y r)
        (addero 0 x y r)
        (== `(,x ,y ,r) s)))
    `((_.0 () _.0)
      (() (_.0 . _.1) (_.0 . _.1))
      ((1) (1) (0 1))
      ((1) (0 _.0 . _.1) (1 _.0 . _.1))
      ((0 _.0 . _.1) (1) (1 _.0 . _.1))
      ((1) (1 1) (0 0 1))
      ((0 1) (0 1) (0 0 1))
      ((1) (1 0 _.0 . _.1) (0 1 _.0 . _.1))
      ((1 1) (1) (0 0 1))
      ((1) (1 1 1) (0 0 0 1))
      ((1 1) (0 1) (1 0 1))
      ((1) (1 1 0 _.0 . _.1) (0 0 1 _.0 . _.1))
      ((1 0 _.0 . _.1) (1) (0 1 _.0 . _.1))
      ((1) (1 1 1 1) (0 0 0 0 1))
      ((0 1) (0 0 _.0 . _.1) (0 1 _.0 . _.1))
      ((1) (1 1 1 0 _.0 . _.1) (0 0 0 1 _.0 . _.1))
      ((1 1 1) (1) (0 0 0 1))
      ((1) (1 1 1 1 1) (0 0 0 0 0 1))
      ((0 1) (1 1) (1 0 1))
      ((1) (1 1 1 1 0 _.0 . _.1) (0 0 0 0 1 _.0 . _.1))
      ((1 1 0 _.0 . _.1) (1) (0 0 1 _.0 . _.1))
      ((1) (1 1 1 1 1 1) (0 0 0 0 0 0 1))))

  (test-check &quot;7.120&quot;
    (run* (s)
      (gen-addero 1 &apos;(0 1 1) &apos;(1 1) s))
    (list `(0 1 0 1)))

  (test-check &quot;7.126&quot;
    (run* (s)
      (fresh (x y)
        (addero 0 x y &apos;(1 0 1))
        (== `(,x ,y) s)))
    `(((1 0 1) ())
      (() (1 0 1))
      ((1) (0 0 1))
      ((0 0 1) (1))
      ((1 1) (0 1))
      ((0 1) (1 1))))

  ; 7.128
  (define +o
    (lambda (n m k)
      (addero 0 n m k)))

  (test-check &quot;7.129&quot;
    (run* (s)
      (fresh (x y)
        (+o x y &apos;(1 0 1))
        (== `(,x ,y) s)))
    `(((1 0 1) ())
      (() (1 0 1))
      ((1) (0 0 1))
      ((0 0 1) (1))
      ((1 1) (0 1))
      ((0 1) (1 1))))

  ; 7.130
  (define -o
    (lambda (n m k)
      (+o m k n)))

  (test-check &quot;7.131&quot;
    (run* (q)
      (-o &apos;(0 0 0 1) &apos;(1 0 1) q))
    `((1 1)))

  (test-check &quot;7.132&quot;
    (run* (q)
      (-o &apos;(0 1 1) &apos;(0 1 1) q))
    `(()))

  (test-check &quot;7.133&quot;
    (run* (q)
      (-o &apos;(0 1 1) &apos;(0 0 0 1) q))
    `())

  ; 8.10
  (define *o
    (lambda (n m p)
      (condi
        ((== &apos;() n) (== &apos;() p))
        ((poso n) (== &apos;() m) (== &apos;() p))
        ((== &apos;(1) n) (poso m) (== m p))
        ((&gt;1o n) (== &apos;(1) m) (== n p))
        ((fresh (x z)
           (== `(0 . ,x) n) (poso x)
           (== `(0 . ,z) p) (poso z)
           (&gt;1o m)
           (*o x m z)))
        ((fresh (x y)
           (== `(1 . ,x) n) (poso x)
           (== `(0 . ,y) m) (poso y)
           (*o m n p)))
        ((fresh (x y)
            (== `(1 . ,x) n) (poso x)
            (== `(1 . ,y) m) (poso y)
            (odd-*o x n m p)))
        (else fail))))

  ; 8.18
  (define odd-*o
    (lambda (x n m p)
      (fresh (q)
        (bound-*o q p n m)
        (*o x m q)
        (+o `(0 . ,q) m p))))

  (define bound-*o
    (lambda (q p n m)
      (conde
        ((nullo q) (pairo p))
        (else
          (fresh (x y z)
            (cdro q x)
            (cdro p y)
            (condi
              ((nullo n)
               (cdro m z)
               (bound-*o x y z &apos;()))
              (else
                (cdro n z)
                (bound-*o x y z m))))))))

  (test-check &quot;8.1&quot;
    (run 34 (t)
      (fresh (x y r)
        (*o x y r)
        (== `(,x ,y ,r) t)))
    `((() _.0 ())
      ((_.0 . _.1) () ())
      ((1) (_.0 . _.1) (_.0 . _.1))
      ((_.0 _.1 . _.2) (1) (_.0 _.1 . _.2))
      ((0 1) (_.0 _.1 . _.2) (0 _.0 _.1 . _.2))
      ((1 _.0 . _.1) (0 1) (0 1 _.0 . _.1))
      ((0 0 1) (_.0 _.1 . _.2) (0 0 _.0 _.1 . _.2))
      ((1 1) (1 1) (1 0 0 1))
      ((0 1 _.0 . _.1) (0 1) (0 0 1 _.0 . _.1))
      ((1 _.0 . _.1) (0 0 1) (0 0 1 _.0 . _.1))
      ((0 0 0 1) (_.0 _.1 . _.2) (0 0 0 _.0 _.1 . _.2))
      ((1 1) (1 0 1) (1 1 1 1))
      ((0 1 1) (1 1) (0 1 0 0 1))
      ((1 1) (0 1 1) (0 1 0 0 1))
      ((0 0 1 _.0 . _.1) (0 1) (0 0 0 1 _.0 . _.1))
      ((1 1) (1 1 1) (1 0 1 0 1))
      ((0 1 _.0 . _.1) (0 0 1) (0 0 0 1 _.0 . _.1))
      ((1 _.0 . _.1) (0 0 0 1) (0 0 0 1 _.0 . _.1))
      ((0 0 0 0 1) (_.0 _.1 . _.2) (0 0 0 0 _.0 _.1 . _.2))
      ((1 0 1) (1 1) (1 1 1 1))
      ((0 1 1) (1 0 1) (0 1 1 1 1))
      ((1 0 1) (0 1 1) (0 1 1 1 1))
      ((0 0 1 1) (1 1) (0 0 1 0 0 1))
      ((1 1) (1 0 0 1) (1 1 0 1 1))
      ((0 1 1) (0 1 1) (0 0 1 0 0 1))
      ((1 1) (0 0 1 1) (0 0 1 0 0 1))
      ((0 0 0 1 _.0 . _.1) (0 1) (0 0 0 0 1 _.0 . _.1))
      ((1 1) (1 1 0 1) (1 0 0 0 0 1))
      ((0 1 1) (1 1 1) (0 1 0 1 0 1))
      ((1 1 1) (0 1 1) (0 1 0 1 0 1))
      ((0 0 1 _.0 . _.1) (0 0 1) (0 0 0 0 1 _.0 . _.1))
      ((1 1) (1 0 1 1) (1 1 1 0 0 1))
      ((0 1 _.0 . _.1) (0 0 0 1) (0 0 0 0 1 _.0 . _.1))
      ((1 _.0 . _.1) (0 0 0 0 1) (0 0 0 0 1 _.0 . _.1))))

  (test-check &quot;8.4&quot;
    (run* (p)
      (*o &apos;(0 1) &apos;(0 0 1) p))
    (list `(0 0 0 1)))

  ; 8.19
  (define bound-*o
    (lambda (q p n m)
      succeed))

  (test-check &quot;8.20&quot;
    (run 1 (t)
      (fresh (n m)
        (*o n m &apos;(1))
        (== `(,n ,m) t)))
    (list `((1) (1))))

  (test-divergence &quot;8.21&quot;
    (run 2 (t)
      (fresh (n m)
        (*o n m &apos;(1))
        (== `(,n ,m) t))))

  ; 8.22
  (define bound-*o
    (lambda (q p n m)
      (conde
        ((nullo q) (pairo p))
        (else
          (fresh (x y z)
            (cdro q x)
            (cdro p y)
            (condi
              ((nullo n)
               (cdro m z)
               (bound-*o x y z &apos;()))
              (else
                (cdro n z)
                (bound-*o x y z m))))))))

  (test-check &quot;8.23&quot;
    (run 2 (t)
      (fresh (n m)
        (*o n m &apos;(1))
        (== `(,n ,m) t)))
    `(((1) (1))))

  (test-check &quot;8.24&quot;
    (run* (p)
      (*o &apos;(1 1 1) &apos;(1 1 1 1 1 1) p))
    (list `(1 0 0 1 1 1 0 1 1)))

  ; 8.26
  (define =lo
    (lambda (n m)
      (conde
        ((== &apos;() n) (== &apos;() m))
        ((== &apos;(1) n) (== &apos;(1) m))
        (else
          (fresh (a x b y)
            (== `(,a . ,x) n) (poso x)
            (== `(,b . ,y) m) (poso y)
            (=lo x y))))))

  (test-check &quot;8.27&quot;
    (run* (t)
      (fresh (w x y)
        (=lo `(1 ,w ,x . ,y) &apos;(0 1 1 0 1))
        (== `(,w ,x ,y) t)))
    (list `(_.0 _.1 (_.2 1))))

  (test-check &quot;8.28&quot;
    (run* (b)
      (=lo &apos;(1) `(,b)))
    (list 1))

  (test-check &quot;8.29&quot;
    (run* (n)
      (=lo `(1 0 1 . ,n) &apos;(0 1 1 0 1)))
    (list `(_.0 1)))

  (test-check &quot;8.30&quot;
    (run 5 (t)
      (fresh (y z)
        (=lo `(1 . ,y) `(1 . ,z))
        (== `(,y ,z) t)))
    `((() ())
      ((1) (1))
      ((_.0 1) (_.1 1))
      ((_.0 _.1 1) (_.2 _.3 1))
      ((_.0 _.1 _.2 1) (_.3 _.4 _.5 1))))

  (test-check &quot;8.31&quot;
    (run 5 (t)
      (fresh (y z)
        (=lo `(1 . ,y) `(0 . ,z))
        (== `(,y ,z) t)))
    `(((1) (1))
      ((_.0 1) (_.1 1))
      ((_.0 _.1 1) (_.2 _.3 1))
      ((_.0 _.1 _.2 1) (_.3 _.4 _.5 1))
      ((_.0 _.1 _.2 _.3 1) (_.4 _.5 _.6 _.7 1))))

  (test-check &quot;8.33&quot;
    (run 5 (t)
      (fresh (y z)
        (=lo `(1 . ,y) `(0 1 1 0 1 . ,z))
        (== `(,y ,z) t)))
    `(((_.0 _.1 _.2 1) ())
      ((_.0 _.1 _.2 _.3 1) (1))
      ((_.0 _.1 _.2 _.3 _.4 1) (_.5 1))
      ((_.0 _.1 _.2 _.3 _.4 _.5 1) (_.6 _.7 1))
      ((_.0 _.1 _.2 _.3 _.4 _.5 _.6 1) (_.7 _.8 _.9 1))))

  ; 8.34
  (define &lt;lo
    (lambda (n m)
      (conde
        ((== &apos;() n) (poso m))
        ((== &apos;(1) n) (&gt;1o m))
        (else
          (fresh (a x b y)
            (== `(,a . ,x) n) (poso x)
            (== `(,b . ,y) m) (poso y)
            (&lt;lo x y))))))

  (test-check &quot;8.35&quot;
    (run 8 (t)
      (fresh (y z)
        (&lt;lo `(1 . ,y) `(0 1 1 0 1 . ,z))
        (== `(,y ,z) t)))
    `((() _.0)
      ((1) _.0)
      ((_.0 1) _.1)
      ((_.0 _.1 1) _.2)
      ((_.0 _.1 _.2 1) (_.3 . _.4))
      ((_.0 _.1 _.2 _.3 1) (_.4 _.5 . _.6))
      ((_.0 _.1 _.2 _.3 _.4 1) (_.5 _.6 _.7 . _.8))
      ((_.0 _.1 _.2 _.3 _.4 _.5 1) (_.6 _.7 _.8 _.9 . _.10))))

  (test-divergence &quot;8.37&quot;
    (run 1 (n)
      (&lt;lo n n)))

  ; 8.38
  (define &lt;=lo
    (lambda (n m)
      (conde
        ((=lo n m) succeed)
        ((&lt;lo n m) succeed)
        (else fail))))

  (test-check &quot;8.39&quot;
    (run 8 (t)
      (fresh (n m)
        (&lt;=lo n m)
        (== `(,n ,m) t)))
    `((() ())
      ((1) (1))
      ((_.0 1) (_.1 1))
      ((_.0 _.1 1) (_.2 _.3 1))
      ((_.0 _.1 _.2 1) (_.3 _.4 _.5 1))
      ((_.0 _.1 _.2 _.3 1) (_.4 _.5 _.6 _.7 1))
      ((_.0 _.1 _.2 _.3 _.4 1) (_.5 _.6 _.7 _.8 _.9 1))
      ((_.0 _.1 _.2 _.3 _.4 _.5 1) (_.6 _.7 _.8 _.9 _.10 _.11 1))))

  (test-check &quot;8.40&quot;
    (run 1 (t)
      (fresh (n m)
        (&lt;=lo n m)
        (*o n &apos;(0 1) m)
        (== `(,n ,m) t)))
    (list `(() ())))

  (test-divergence &quot;8.41&quot;
    (run 2 (t)
      (fresh (n m)
        (&lt;=lo n m)
        (*o n &apos;(0 1) m)
        (== `(,n ,m) t))))

  (test-divergence &quot;8-18&quot;
    (run 2 (t)
      (fresh (n m)
        (&lt;=lo n m)
        (*o n &apos;(0 1) m)
        (== `(,n ,m) t))))

  ; 8.42
  (define &lt;=lo
    (lambda (n m)
      (condi
        ((=lo n m) succeed)
        ((&lt;lo n m) succeed)
        (else fail))))

  (test-check &quot;8.43&quot;
    (run 10 (t)
      (fresh (n m)
        (&lt;=lo n m)
        (*o n &apos;(0 1) m)
        (== `(,n ,m) t)))
    `((() ())
      ((1) (0 1))
      ((0 1) (0 0 1))
      ((1 1) (0 1 1))
      ((0 0 1) (0 0 0 1))
      ((1 _.0 1) (0 1 _.0 1))
      ((0 1 1) (0 0 1 1))
      ((0 0 0 1) (0 0 0 0 1))
      ((1 _.0 _.1 1) (0 1 _.0 _.1 1))
      ((0 1 _.0 1) (0 0 1 _.0 1))))

  (test-check &quot;8.44&quot;
    (run 15 (t)
      (fresh (n m)
        (&lt;=lo n m)
        (== `(,n ,m) t)))
    `((() ())
      (() (_.0 . _.1))
      ((1) (1))
      ((1) (_.0 _.1 . _.2))
      ((_.0 1) (_.1 1))
      ((_.0 1) (_.1 _.2 _.3 . _.4))
      ((_.0 _.1 1) (_.2 _.3 1))
      ((_.0 _.1 1) (_.2 _.3 _.4 _.5 . _.6))
      ((_.0 _.1 _.2 1) (_.3 _.4 _.5 1))
      ((_.0 _.1 _.2 1) (_.3 _.4 _.5 _.6 _.7 . _.8))
      ((_.0 _.1 _.2 _.3 1) (_.4 _.5 _.6 _.7 1))
      ((_.0 _.1 _.2 _.3 1) (_.4 _.5 _.6 _.7 _.8 _.9 . _.10))
      ((_.0 _.1 _.2 _.3 _.4 1) (_.5 _.6 _.7 _.8 _.9 1))
      ((_.0 _.1 _.2 _.3 _.4 1) (_.5 _.6 _.7 _.8 _.9 _.10 _.11 . _.12))
      ((_.0 _.1 _.2 _.3 _.4 _.5 1) (_.6 _.7 _.8 _.9 _.10 _.11 1))))

  ; 8.46.1
  (define &lt;o
    (lambda (n m)
      (condi
        ((&lt;lo n m) succeed)
        ((=lo n m)
         (fresh (x)
           (poso x)
           (+o n x m)))
        (else fail))))

  ; 8.46.2
  (define &lt;=o
    (lambda (n m)
      (condi
        ((== n m) succeed)
        ((&lt;o n m) succeed)
        (else fail))))

  (test-check &quot;8.47&quot;
    (run* (q)
      (&lt;o &apos;(1 0 1) &apos;(1 1 1))
      (== #t q))
    (list #t))

  (test-check &quot;8.48&quot;
    (run* (q)
      (&lt;o &apos;(1 1 1) &apos;(1 0 1))
      (== #t q))
    `())

  (test-check &quot;8.49.1&quot;
    (run* (q)
      (&lt;o &apos;(1 0 1) &apos;(1 0 1))
      (== #t q))
    `())

  (test-check &quot;8.49.2&quot;
    (run* (q)
      (&lt;=o &apos;(1 0 1) &apos;(1 0 1))
      (== #t q))
    `(#t))

  (test-check &quot;8.50&quot;
    (run 6 (n)
      (&lt;o n `(1 0 1)))
    `(() (0 0 1) (1) (_.0 1)))

  (test-check &quot;8.51&quot;
    (run 6 (m)
      (&lt;o `(1 0 1) m))
    `((_.0 _.1 _.2 _.3 . _.4) (0 1 1) (1 1 1)))

  (test-divergence &quot;8.52&quot;
    (run* (n)
      (&lt;o n n)))

  (define /o
    (lambda (n m q r)
      (condi
        ((== r n) (== &apos;() q) (&lt;o n m))
        ((== &apos;(1) q) (=lo n m) (+o r m n)
         (&lt;o r m))
        (else
          (alli
            (&lt;lo m n)
            (&lt;o r m)
            (poso q)
            (fresh (nh nl qh ql qlm qlmr rr rh)
              (alli
                (splito n r nl nh)
                (splito q r ql qh)
                (conde
                  ((== &apos;() nh)
                   (== &apos;() qh)
                   (-o nl r qlm)
                   (*o ql m qlm))
                  (else
                    (alli
                      (poso nh)
                      (*o ql m qlm)
                      (+o qlm r qlmr)
                      (-o qlmr nl rr)
                      (splito rr r &apos;() rh)
                      (/o nh m qh rh)))))))))))

  (define splito
    (lambda (n r l h)
      (condi
        ((== &apos;() n) (== &apos;() h) (== &apos;() l))
        ((fresh (b n^)
           (== `(0 ,b . ,n^) n)
           (== &apos;() r)
           (== `(,b . ,n^) h)
           (== &apos;() l)))
        ((fresh (n^)
           (==  `(1 . ,n^) n)
           (== &apos;() r)
           (== n^ h)
           (== &apos;(1) l)))
        ((fresh (b n^ a r^)
           (== `(0 ,b . ,n^) n)
           (== `(,a . ,r^) r)
           (== &apos;() l)
           (splito `(,b . ,n^) r^ &apos;() h)))
        ((fresh (n^ a r^)
           (== `(1 . ,n^) n)
           (== `(,a . ,r^) r)
           (== &apos;(1) l)
           (splito n^ r^ &apos;() h)))
        ((fresh (b n^ a r^ l^)
           (== `(,b . ,n^) n)
           (== `(,a . ,r^) r)
           (== `(,b . ,l^) l)
           (poso l^)
           (splito n^ r^ l^ h)))
        (else fail))))

  (test-check &quot;8.53&quot;
    (run 15 (t)
      (fresh (n m q r)
        (/o n m q r)
        (== `(,n ,m ,q ,r) t)))
    `((() (_.0 . _.1) () ())
      ((1) (1) (1) ())
      ((0 1) (1 1) () (0 1))
      ((0 1) (1) (0 1) ())
      ((1) (_.0 _.1 . _.2) () (1))
      ((_.0 1) (_.0 1) (1) ())
      ((0 _.0 1) (1 _.0 1) () (0 _.0 1))
      ((0 _.0 1) (_.0 1) (0 1) ())
      ((_.0 1) (_.1 _.2 _.3 . _.4) () (_.0 1))
      ((1 1) (0 1) (1) (1))
      ((0 0 1) (0 1 1) () (0 0 1))
      ((1 1) (1) (1 1) ())
      ((_.0 _.1 1) (_.2 _.3 _.4 _.5 . _.6) () (_.0 _.1 1))
      ((_.0 _.1 1) (_.0 _.1 1) (1) ())
      ((1 0 1) (0 1 1) () (1 0 1))))

  ; 8.63
  (define /o
    (lambda (n m q r)
      (condi
        ((== &apos;() q) (== n r) (&lt;o n m))
        ((== &apos;(1) q) (== &apos;() r) (== n m)
         (&lt;o r m))
        ((&lt;o m n) (&lt;o r m)
         (fresh (mq)
           (&lt;=lo mq n)
           (*o m q mq)
           (+o mq r n)))
        (else fail))))

  (test-divergence &quot;8.81&quot;
    (run 3 (t)
      (fresh (y z)
        (/o `(1 0 . ,y) &apos;(0 1) z &apos;())
        (== `(,y ,z) t))))

  ; 8.76.1
  (define /o
    (lambda (n m q r)
      (condi
        ((== r n) (== &apos;() q) (&lt;o n m))
        ((== &apos;(1) q) (=lo n m) (+o r m n)
         (&lt;o r m))
        (else
          (alli
            (&lt;lo m n)
            (&lt;o r m)
            (poso q)
            (fresh (nh nl qh ql qlm qlmr rr rh)
              (alli
                (splito n r nl nh)
                (splito q r ql qh)
                (conde
                  ((== &apos;() nh)
                   (== &apos;() qh)
                   (-o nl r qlm)
                   (*o ql m qlm))
                  (else
                    (alli
                      (poso nh)
                      (*o ql m qlm)
                      (+o qlm r qlmr)
                      (-o qlmr nl rr)
                      (splito rr r &apos;() rh)
                      (/o nh m qh rh)))))))))))

  ; 8.76.2
  (define splito
    (lambda (n r l h)
      (condi
        ((== &apos;() n) (== &apos;() h) (== &apos;() l))
        ((fresh (b n^)
           (== `(0 ,b . ,n^) n)
           (== &apos;() r)
           (== `(,b . ,n^) h)
           (== &apos;() l)))
        ((fresh (n^)
           (==  `(1 . ,n^) n)
           (== &apos;() r)
           (== n^ h)
           (== &apos;(1) l)))
        ((fresh (b n^ a r^)
           (== `(0 ,b . ,n^) n)
           (== `(,a . ,r^) r)
           (== &apos;() l)
           (splito `(,b . ,n^) r^ &apos;() h)))
        ((fresh (n^ a r^)
           (== `(1 . ,n^) n)
           (== `(,a . ,r^) r)
           (== &apos;(1) l)
           (splito n^ r^ &apos;() h)))
        ((fresh (b n^ a r^ l^)
           (== `(,b . ,n^) n)
           (== `(,a . ,r^) r)
           (== `(,b . ,l^) l)
           (poso l^)
           (splito n^ r^ l^ h)))
        (else fail))))

  ; 8.82.1
  (define logo
    (lambda (n b q r)
      (condi
        ((== &apos;(1) n) (poso b) (== &apos;() q) (== &apos;() r))
        ((== &apos;() q) (&lt;o n b) (+o r &apos;(1) n))
        ((== &apos;(1) q) (&gt;1o b) (=lo n b) (+o r b n))
        ((== &apos;(1) b) (poso q) (+o r &apos;(1) n))
        ((== &apos;() b) (poso q) (== r n))
        ((== &apos;(0 1) b)
         (fresh (a ad dd)
           (poso dd)
           (== `(,a ,ad . ,dd) n)
           (exp2 n &apos;() q)
           (fresh (s)
             (splito n dd r s))))
        ((fresh (a ad add ddd)
           (conde
             ((== &apos;(1 1) b))
             (else (== `(,a ,ad ,add . ,ddd) b))))
         (&lt;lo b n)
         (fresh (bw1 bw nw nw1 ql1 ql s)
           (exp2 b &apos;() bw1)
           (+o bw1 &apos;(1) bw)
           (&lt;lo q n)
           (fresh (q1 bwq1)
             (+o q &apos;(1) q1)
             (*o bw q1 bwq1)
             (&lt;o nw1 bwq1))
             (exp2 n &apos;() nw1)
             (+o nw1 &apos;(1) nw)
             (/o nw bw ql1 s)
             (+o ql &apos;(1) ql1)
           (conde
             ((== q ql))
             (else (&lt;lo ql q)))
           (fresh (bql qh s qdh qd)
             (repeated-mul b ql bql)
             (/o nw bw1 qh s)
             (+o ql qdh qh)
             (+o ql qd q)
             (conde
               ((== qd qdh))
               (else (&lt;o qd qdh)))
             (fresh (bqd bq1 bq)
               (repeated-mul b qd bqd)
               (*o bql bqd bq)
               (*o b bq bq1)
               (+o bq r n)
               (&lt;o n bq1)))))
        (else fail))))

  ; 8.82.2
  (define exp2
    (lambda (n b q)
      (condi
        ((== &apos;(1) n) (== &apos;() q))
        ((&gt;1o n) (== &apos;(1) q)
         (fresh (s)
           (splito n b s &apos;(1))))
        ((fresh (q1 b2)
           (alli
             (== `(0 . ,q1) q)
             (poso q1)
             (&lt;lo b n)
             (appendo b `(1 . ,b) b2)
             (exp2 n b2 q1))))
        ((fresh (q1 nh b2 s)
            (alli
              (== `(1 . ,q1) q)
              (poso q1)
              (poso nh)
              (splito n b s nh)
              (appendo b `(1 . ,b) b2)
              (exp2 nh b2 q1))))
        (else fail))))

  ; 8.82.3
  (define repeated-mul
    (lambda (n q nq)
      (conde
        ((poso n) (== &apos;() q) (== &apos;(1) nq))
        ((== &apos;(1) q) (== n nq))
        ((&gt;1o q)
         (fresh (q1 nq1)
           (+o q1 &apos;(1) q)
           (repeated-mul n q1 nq1)
           (*o nq1 n nq)))
        (else fail))))

  (test-check &quot;8.89&quot;
    (run* (r)
      (logo &apos;(0 1 1 1) &apos;(0 1) &apos;(1 1) r))
    (list `(0 1 1)))

  (cout &quot;This next test takes several minutes to run!&quot; nl)

  (test-check &quot;8.96&quot;
    (run 8 (s)
      (fresh (b q r)
        (logo &apos;(0 0 1 0 0 0 1) b q r)
        (&gt;1o q)
        (== `(,b ,q ,r) s)))
    `(((1) (_.0 _.1 . _.2) (1 1 0 0 0 0 1))
      (() (_.0 _.1 . _.2) (0 0 1 0 0 0 1))
      ((0 1) (0 1 1) (0 0 1))
      ((0 0 1) (1 1) (0 0 1))
      ((1 0 1) (0 1) (1 1 0 1 0 1))
      ((0 1 1) (0 1) (0 0 0 0 0 1))
      ((1 1 1) (0 1) (1 1 0 0 1))
      ((0 0 0 1) (0 1) (0 0 1))))

  ; 8.91
  (define expo
    (lambda (b q n)
      (logo n b q &apos;())))

  (test-check &quot;8.92&quot;
    (run* (t)
      (expo &apos;(1 1) &apos;(1 0 1) t))
    (list `(1 1 0 0 1 1 1 1)))

  ; 9.6
  (define u (var &apos;u))
  (define v (var &apos;v))
  (define w (var &apos;w))
  (define x (var &apos;x))
  (define y (var &apos;y))
  (define z (var &apos;z))

  (test-check &quot;9.8&quot;
    (rhs `(,z . b))
    &apos;b)

  (test-check &quot;9.9&quot;
    (rhs `(,z . ,w))
    w)

  (test-check &quot;9.10&quot;
    (rhs `(,z . (,x e ,y)))
    `(,x e ,y))

  (test-check &quot;9.14&quot;
    (walk z `((,z . a) (,x . ,w) (,y . ,z)))
    &apos;a)

  (test-check &quot;9.15&quot;
    (walk y `((,z . a) (,x . ,w) (,y . ,z)))
    &apos;a)

  (test-check &quot;9.16&quot;
    (walk x `((,z . a) (,x . ,w) (,y . ,z)))
    w)

  (test-check &quot;9.17&quot;
    (walk w `((,z . a) (,x . ,w) (,y . ,z)))
    w)

  (test-divergence &quot;9.18&quot;
    (walk x `((,x . ,y) (,z . ,x) (,y . ,z))))

  (test-check &quot;9.19&quot;
    (walk w `((,x . ,y) (,w . b) (,z . ,x) (,y . ,z)))
    &apos;b)

  (test-check &quot;9.25&quot;
    (walk u `((,x . b) (,w . (,x e ,x)) (,u . ,w)))
    `(,x e ,x))

  (test-divergence &quot;9.29&quot;
    (walk x (ext-s x y `((,z . ,x) (,y . ,z)))))

  (test-check &quot;9.30&quot;
    (walk y `((,x . e)))
    y)

  (test-check &quot;9.31&quot;
    (walk y (ext-s y x `((,x . e))))
    &apos;e)

  (test-check &quot;9.32&quot;
    (walk x `((,y . ,z) (,x . ,y)))
    z)

  (test-check &quot;9.33&quot;
    (walk x (ext-s z &apos;b `((,y . ,z) (,x . ,y))))
    &apos;b)

  (test-check &quot;9.34&quot;
    (walk x (ext-s z w `((,y . ,z) (,x . ,y))))
    w)

  (test-check &quot;9.44&quot;
    (walk* x `((,y . (a ,z c)) (,x . ,y) (,z . a)))
    `(a a c))

  (test-check &quot;9.45&quot;
    (walk* x `((,y . (,z ,w c)) (,x . ,y) (,z . a)))
    `(a ,w c))

  (test-check &quot;9.46&quot;
    (walk* y `((,y . (,w ,z c)) (,v . b) (,x . ,v) (,z . ,x)))
    `(,w b c))

  (test-check &quot;9.47&quot;
    (run* (q)
      (== #f q)
      (project (q)
        (== (not (not q)) q)))
    &apos;(#f))

  (test-check &quot;9.53&quot;
    (let ((r `(,w ,x ,y)))
      (walk* r (reify-s r empty-s)))
    `(_.0 _.1 _.2))

  (test-check &quot;9.54&quot;
    (let ((r (walk* `(,x ,y ,z) empty-s)))
      (walk* r (reify-s r empty-s)))
    `(_.0 _.1 _.2))

  (test-check &quot;9.55&quot;
    (let ((r `(,u (,v (,w ,x) ,y) ,x)))
      (walk* r (reify-s r empty-s)))
    `(_.0 (_.1 (_.2 _.3) _.4) _.3))

  (test-check &quot;9.56&quot;
    (let ((s `((,y . (,z ,w c ,w)) (,x . ,y) (,z . a))))
      (let ((r (walk* x s)))
        (walk* r (reify-s r empty-s))))
    `(a _.0 c _.0))

  (test-check &quot;9.58&quot;
    (let ((s `((,y . (,z ,w c ,w)) (,x . ,y) (,z . a))))
      (reify (walk* x s)))
    `(a _.0 c _.0))

  (test-divergence &quot;9.61&quot;
    (run 1 (x)
      (== `(,x) x)))

  (test-check &quot;9.62&quot;
    (run 1 (q)
      (fresh (x)
        (== `(,x) x)
        (== #t q)))
    `(#t))

  (test-check &quot;9.63&quot;
    (run 1 (q)
      (fresh (x y)
        (== `(,x) y)
        (== `(,y) x)
        (== #t q)))
    `(#t))

  (test-check &quot;9.64&quot;
    (run 1 (x)
      (==-check `(,x) x))
    `())

  (test-divergence &quot;9.65&quot;
    (run 1 (x)
      (fresh (y z)
        (== x z)
        (== `(a b ,z) y)
        (== x y))))

  (test-check &quot;9.66&quot;
    (run 1 (x)
      (fresh (y z)
        (== x z)
        (== `(a b ,z) y)
        (==-check x y)))
    `())

  (test-divergence &quot;9.69&quot;
    (run 1 (x)
      (== `(,x) x)))

  (test-check &quot;10.1&quot;
    (run* (q)
      (conda
        (fail succeed)
        (else fail)))
    &apos;())

  (test-check &quot;10.2&quot;
    (not (null? (run* (q)
                  (conda
                    (fail succeed)
                    (else succeed)))))
    #t)

  (test-check &quot;10.3&quot;
    (not (null? (run* (q)
                  (conda
                    (succeed fail)
                    (else succeed)))))
    #f)

  (test-check &quot;10.4&quot;
    (not (null? (run* (q)
                  (conda
                    (succeed succeed)
                    (else fail)))))
    #t)

  (test-check &quot;10.5&quot;
    (run* (x)
      (conda
        ((== &apos;olive x) succeed)
        ((== &apos;oil x) succeed)
        (else fail)))
    `(olive))

  (test-check &quot;10.7&quot;
    (run* (x)
      (conda
        ((== &apos;virgin x) fail)
        ((== &apos;olive x) succeed)
        ((== &apos;oil x) succeed)
        (else fail)))
    `())

  (test-check &quot;10.8&quot;
    (run* (q)
      (fresh (x y)
        (== &apos;split x)
        (== &apos;pea y)
        (conda
          ((== &apos;split x) (== x y))
          (else succeed)))
      (== #t q))
    `())

  (test-check &quot;10.9&quot;
    (run* (q)
      (fresh (x y)
        (== &apos;split x)
        (== &apos;pea y)
        (conda
          ((== x y) (== &apos;split x))
          (else succeed)))
      (== #t q))
    (list #t))

  ; 10.11.1
  (define not-pastao
    (lambda (x)
      (conda
        ((== &apos;pasta x) fail)
        (else succeed))))

  (test-check &quot;10.11.2&quot;
    (run* (x)
      (conda
        ((not-pastao x) fail)
        (else (== &apos;spaghetti x))))
    &apos;(spaghetti))

  (test-check &quot;10.12&quot;
    (run* (x)
      (== &apos;spaghetti x)
      (conda
        ((not-pastao x) fail)
        (else (== &apos;spaghetti x))))
    &apos;())

  (test-divergence &quot;10.13&quot;
    (run* (q)
      (conda
        (alwayso succeed)
        (else fail))
      (== #t q)))

  (test-check &quot;10.14&quot;
    (run* (q)
      (condu
        (alwayso succeed)
        (else fail))
      (== #t q))
    `(#t))

  (test-divergence &quot;10.15&quot;
    (run* (q)
      (condu
        (succeed alwayso)
        (else fail))
      (== #t q)))

  (test-divergence &quot;10.17&quot;
    (run 1 (q)
      (conda
        (alwayso succeed)
        (else fail))
      fail
      (== #t q)))

  (test-check &quot;10.18&quot;
    (run 1 (q)
      (condu
        (alwayso succeed)
        (else fail))
      fail
      (== #t q))
    `())

  ; 10.19.1
  (define onceo
    (lambda (g)
      (condu
        (g succeed)
        (else fail))))

  (test-check &quot;10.19.2&quot;
    (run* (x)
      (onceo (teacupo x)))
    `(tea))

  (test-check &quot;10.20&quot;
    (run 1 (q)
      (onceo (salo nevero))
      fail)
    `())

  (test-check &quot;10.21&quot;
    (run* (r)
      (conde
        ((teacupo r) succeed)
        ((== #f r) succeed)
        (else fail)))
    `(tea cup #f))

  (test-check &quot;10.22&quot;
    (run* (r)
      (conda
        ((teacupo r) succeed)
        ((== #f r) succeed)
        (else fail)))
    `(tea cup))

  (test-check &quot;10.23&quot;
    (run* (r)
      (== #f r)
      (conda
        ((teacupo r) succeed)
        ((== #f r) succeed)
        (else fail)))
    `(#f))

  (test-check &quot;10.24&quot;
    (run* (r)
      (== #f r)
      (condu
        ((teacupo r) succeed)
        ((== #f r) succeed)
        (else fail)))
    `(#f))

  ; 10.26.1
  (define bumpo
    (lambda (n x)
      (conde
        ((== n x) succeed)
        (else
          (fresh (m)
            (-o n &apos;(1) m)
            (bumpo m x))))))

  (test-check &quot;10.26.2&quot;
    (run* (x)
      (bumpo &apos;(1 1 1) x))
    `((1 1 1)
      (0 1 1)
      (1 0 1)
      (0 0 1)
      (1 1)
      (0 1)
      (1)
      ()))

  ; 10.27.1
  (define gen&amp;testo
    (lambda (op i j k)
      (onceo
        (fresh (x y z)
          (op x y z)
          (== i x)
          (== j y)
          (== k z)))))

  (test-check &quot;10.27.2&quot;
    (run* (q)
      (gen&amp;testo +o &apos;(0 0 1) &apos;(1 1) &apos;(1 1 1))
      (== #t q))
    (list #t))

  (test-divergence &quot;10.42&quot;
    (run 1 (q)
      (gen&amp;testo +o &apos;(0 0 1) &apos;(1 1) &apos;(0 1 1))))

  ; 10.43.1
  (define enumerateo
    (lambda (op r n)
      (fresh (i j k)
        (bumpo n i)
        (bumpo n j)
        (op i j k)
        (gen&amp;testo op i j k)
        (== `(,i ,j ,k) r))))

  (test-check &quot;10.43.2&quot;
    (run* (s)
      (enumerateo +o s &apos;(1 1)))
    `(((1 1) (1 1) (0 1 1))
      ((1 1) (0 1) (1 0 1))
      ((1 1) (1) (0 0 1))
      ((1 1) () (1 1))
      ((0 1) (1 1) (1 0 1))
      ((0 1) (0 1) (0 0 1))
      ((0 1) (1) (1 1))
      ((0 1) () (0 1))
      ((1) (1 1) (0 0 1))
      ((1) (0 1) (1 1))
      ((1) (1) (0 1))
      ((1) () (1))
      (() (1 1) (1 1))
      (() (0 1) (0 1))
      (() (1) (1))
      (() () ())))

  (test-check &quot;10.56&quot;
    (run 1 (s)
      (enumerateo +o s &apos;(1 1 1)))
    `(((1 1 1) (1 1 1) (0 1 1 1))))

  ; 10.57
  (define gen-addero
    (lambda (d n m r)
      (fresh (a b c e x y z)
        (== `(,a . ,x) n)
        (== `(,b . ,y) m) (poso y)
        (== `(,c . ,z) r) (poso z)
        (all
          (full-addero d a b c e)
          (addero e x y z)))))

  (test-divergence &quot;10.58&quot;
    (run 1 (q)
      (gen&amp;testo +o &apos;(0 1) &apos;(1 1) &apos;(1 0 1))))

  (test-divergence &quot;10.62&quot;
    (run* (q)
      (enumerateo +o q &apos;(1 1 1))))
  #+end_src
</pre>
</body>
</html>
